#Область ОбработчикиСобытийФормы

&НаКлиенте
Функция Получить(ДанныеГрупп, ОписанияРеквизитов) Экспорт
	Результат = Новый Структура("Реквизиты,Элементы,Данные,ЗаголовокФормы");
	
	Если ДанныеГрупп.НаборСвойств.Свойство("Заголовок") Тогда
		Результат.ЗаголовокФормы = ДанныеГрупп.НаборСвойств.Заголовок;
	КонецЕсли;
	
	Результат.Реквизиты = ПолучитьРеквизитыФормы(ОписанияРеквизитов);

	ПараметрыВыполнения = Новый Структура("ОписанияРеквизитов,Элементы, Данные", ОписанияРеквизитов, Новый Массив, Новый Структура);
	ПостроитьЭлементы(ПараметрыВыполнения, ДанныеГрупп, Неопределено);
	
	Результат.Элементы = ПараметрыВыполнения.Элементы;
	Результат.Данные = ПараметрыВыполнения.Данные;
	
	Возврат Результат;
КонецФункции

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция СлужебныеФункции()
	Возврат ВладелецФормы.СлужебныеФункции();
КонецФункции

&НаКлиенте
Функция ПолучитьОписаниеРеквизита(ИмяРеквизита, ОписаниеТипов)
	Результат = Новый Структура;
	Результат.Вставить("Имя", ИмяРеквизита);
	Результат.Вставить("ОписаниеТипов", ОписаниеТипов);
	Результат.Вставить("Автоудаление", Ложь);
	Результат.Вставить("ИмяВладельца", Неопределено);
	Возврат Результат;
КонецФункции

&НаКлиенте
Функция ПолучитьОписаниеЭлемента(ИмяРеквизита, Тип, ИмяРодителя)
	Результат = Новый Структура;
	Результат.Вставить("Имя", ИмяРеквизита);
	Результат.Вставить("Тип", Тип);
	Результат.Вставить("ИмяРодителя", ИмяРодителя);
	Результат.Вставить("НаборСвойств", Новый Структура);
	Возврат Результат;
КонецФункции

&НаКлиенте
Процедура ДобавитьСвойство(ЭлементФормы, Ключ, Тип, Значение) 
	СтрЗначение = Новый Структура("Тип, Значение");
	СтрЗначение.Тип = Тип;
	СтрЗначение.Значение = Значение;
	ЭлементФормы.НаборСвойств.Вставить(Ключ, СтрЗначение);
КонецПроцедуры

&НаКлиенте
Функция ПолучитьРеквизитыФормы(ОписанияРеквизитов)
	Результат = Новый Массив;

	Для Каждого СтрокаРеквизит Из ОписанияРеквизитов Цикл
		Если ПустаяСтрока(СтрокаРеквизит.ИмяРеквизита) Тогда
			Продолжить;
		КонецЕсли;
		
		ИмяРеквизита = СтрокаРеквизит.ИмяРеквизита;
		Если СтрокаРеквизит.Номер > 1 Тогда
			СтрокаНомер = Формат(СтрокаРеквизит.Номер - 1, "ЧГ=0");
			ИмяРеквизита = ИмяРеквизита + СтрокаНомер;
		КонецЕсли;
		
		Если СтрокаРеквизит.ТипРеквизита = "ПолеВвода" Тогда
			ОписаниеТипов = СтрокаРеквизит.ОписаниеТипов;
			Если ОписаниеТипов.Количество() = 0 Тогда
				ОписаниеТипов.Вставить("Строка", Неопределено);
			КонецЕсли;	
			ОписаниеРеквизита = ПолучитьОписаниеРеквизита(ИмяРеквизита, СтрокаРеквизит.ОписаниеТипов);
		ИначеЕсли СтрокаРеквизит.ТипРеквизита = "Флажок" Тогда
			ОписаниеРеквизита = ПолучитьОписаниеРеквизита(ИмяРеквизита, СтрокаРеквизит.ОписаниеТипов);
		Иначе
			ОписаниеРеквизита = ПолучитьРеквизитыФормыТаблица(ОписанияРеквизитов, СтрокаРеквизит, ИмяРеквизита);
		КонецЕсли;
		
		Результат.Добавить(ОписаниеРеквизита);
	КонецЦикла;
	
	Возврат Результат;
КонецФункции

&НаКлиенте
Функция ПолучитьРеквизитыФормыТаблица(ОписанияРеквизитов, СтрокаРеквизит, ИмяРеквизита) 
	Если СтрокаРеквизит.ТипРеквизита = "Таблица" 
		ИЛИ СтрокаРеквизит.ТипРеквизита = "Дерево" Тогда       
		Возврат ПолучитьОписаниеРеквизита(ИмяРеквизита, СтрокаРеквизит.ОписаниеТипов);
	КонецЕсли;

	СтрокаВладелец = ПолучитьСтрокуТаблицыРеквизитов(ОписанияРеквизитов, СтрокаРеквизит.УИДВладельца);
	
	ОписаниеРеквизита = ПолучитьОписаниеРеквизита(ИмяРеквизита, СтрокаРеквизит.ОписаниеТипов);
	ОписаниеРеквизита.ИмяВладельца = СтрокаВладелец.ИмяРеквизита;
	ОписаниеРеквизита.Автоудаление = Истина;

	Возврат ОписаниеРеквизита;
КонецФункции

&НаКлиенте
Процедура ПостроитьЭлементы(ПараметрыВыполнения, Данные, ИмяГруппы)      
	Подчиненные = Данные.Элементы;
	
	Для Каждого ЭлементДанных Из Подчиненные Цикл
		ПостроитьЭлемент(ПараметрыВыполнения, ЭлементДанных, ИмяГруппы);
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ПостроитьЭлемент(ПараметрыВыполнения, ЭлементДанных, ИмяГруппа)
	Выполнена = Ложь;
	
	ДобавитьГоризонтальнуюГруппу(ПараметрыВыполнения, ЭлементДанных, ИмяГруппа, Выполнена);
	Если Выполнена Тогда
		Возврат;
	КонецЕсли; 

	ДобавитьОднострочнуюГруппу(ПараметрыВыполнения, ЭлементДанных, ИмяГруппа, Выполнена);
	Если Выполнена Тогда
		Возврат;
	КонецЕсли; 

	ДобавитьВертикальнуюГруппу(ПараметрыВыполнения, ЭлементДанных, ИмяГруппа, Выполнена);
	Если Выполнена Тогда
		Возврат;
	КонецЕсли;

	ДобавитьЭлементПолеВвода(ПараметрыВыполнения, ЭлементДанных, ИмяГруппа, Выполнена);
	Если Выполнена Тогда
		Возврат;
	КонецЕсли;

	ДобавитьЭлементФлажок(ПараметрыВыполнения, ЭлементДанных, ИмяГруппа, Выполнена);
	Если Выполнена Тогда
		Возврат;
	КонецЕсли;

	ДобавитьЭлементНадпись(ПараметрыВыполнения, ЭлементДанных, ИмяГруппа, Выполнена);
	Если Выполнена Тогда
		Возврат;
	КонецЕсли;

	ДобавитьТаблицуДерево(ПараметрыВыполнения, ЭлементДанных, ИмяГруппа, Выполнена);
	Если Выполнена Тогда
		Возврат;
	КонецЕсли;

	ДобавитьСтраницы(ПараметрыВыполнения, ЭлементДанных, ИмяГруппа, Выполнена);
	Если Выполнена Тогда
		Возврат;
	КонецЕсли;

	ДобавитьКоманднуюПанель(ПараметрыВыполнения, ЭлементДанных, ИмяГруппа, Выполнена);
	Если Выполнена Тогда
		Возврат;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьГоризонтальнуюГруппу(ПараметрыВыполнения, ЭлементДанных, Группа, Выполнена)  	
	Если ЭлементДанных.Тип <> "ГоризонтальнаяГруппа" Тогда 
		Возврат;
	КонецЕсли;
	СтрокаРеквизитов = ПолучитьСтрокуТаблицыРеквизитов(
		ПараметрыВыполнения.ОписанияРеквизитов, 
		ЭлементДанных.УИД);
	
	ИмяЭлемента = СтрокаРеквизитов.ИмяЭлемента;
	
	НовыйЭлемент = ПолучитьОписаниеЭлемента(ИмяЭлемента, "ГруппаФормы", Группа);
	ПараметрыВыполнения.Элементы.Добавить(НовыйЭлемент);
	
	ДобавитьСвойство(НовыйЭлемент, "Вид", "ВидГруппыФормы", "ОбычнаяГруппа");
	ДобавитьСвойство(НовыйЭлемент, "Отображение", "ОтображениеОбычнойГруппы", "Нет");
	ДобавитьСвойство(НовыйЭлемент, "ОтображатьЗаголовок", "Булево", Ложь);
	ДобавитьСвойство(НовыйЭлемент, "Группировка", "ГруппировкаПодчиненныхЭлементовФормы", "ГоризонтальнаяВсегда");
	ДобавитьСвойство(НовыйЭлемент, "РастягиватьПоГоризонтали", "Булево", Истина);
	ДобавитьСвойство(НовыйЭлемент, "РастягиватьПоВертикали", "Булево", Ложь);
	
	ПостроитьЭлементы(ПараметрыВыполнения, ЭлементДанных, ИмяЭлемента);
	
	Выполнена = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьВертикальнуюГруппу(ПараметрыВыполнения, ЭлементДанных, Группа, Выполнена)  	
	Если ЭлементДанных.Тип <> "ВертикальнаяГруппа" Тогда 
		Возврат;
	КонецЕсли;

	СтрокаРеквизитов = ПолучитьСтрокуТаблицыРеквизитов(
		ПараметрыВыполнения.ОписанияРеквизитов,
		ЭлементДанных.УИД);
	
	ИмяЭлемента = СтрокаРеквизитов.ИмяЭлемента;
	
	НовыйЭлемент = ПолучитьОписаниеЭлемента(ИмяЭлемента, "ГруппаФормы", Группа);
	ПараметрыВыполнения.Элементы.Добавить(НовыйЭлемент);
	
	ДобавитьСвойство(НовыйЭлемент, "Вид", "ВидГруппыФормы", "ОбычнаяГруппа");
	
	УстановитьСвойствоЭлементаНаСервере(НовыйЭлемент, ЭлементДанных);
	
	Если НЕ ЭлементДанных.НаборСвойств.Свойство("Отображение") Тогда   
		ДобавитьСвойство(НовыйЭлемент, "Отображение", "ОтображениеОбычнойГруппы", "Нет");
	КонецЕсли;
	
	Если НЕ ЭлементДанных.НаборСвойств.Свойство("ОтображатьЗаголовок")
		И ПустаяСтрока(ЭлементДанных.НаборСвойств.Заголовок) Тогда
		ДобавитьСвойство(НовыйЭлемент, "ОтображатьЗаголовок", "Булево", Ложь);
	КонецЕсли;

	ДобавитьСвойство(НовыйЭлемент, "Группировка", "ГруппировкаПодчиненныхЭлементовФормы", "Вертикальная");

	ДобавитьСвойство(НовыйЭлемент, "РастягиватьПоГоризонтали", "Булево", Истина);
	ДобавитьСвойство(НовыйЭлемент, "РастягиватьПоВертикали", "Булево", Ложь);
	
	УстановитьСвойствоЭлементаНаСервере(НовыйЭлемент, ЭлементДанных);
	
	ПостроитьЭлементы(ПараметрыВыполнения, ЭлементДанных, ИмяЭлемента);   
	
	Выполнена = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьОднострочнуюГруппу(ПараметрыВыполнения, ЭлементДанных, Группа, Выполнена)  	
	Если ЭлементДанных.Тип <> "ОднострочнаяГруппа" Тогда 
		Возврат;
	КонецЕсли;

	СтрокаРеквизитов = ПолучитьСтрокуТаблицыРеквизитов(
		ПараметрыВыполнения.ОписанияРеквизитов,
		ЭлементДанных.УИД);
	
	ИмяЭлемента = СтрокаРеквизитов.ИмяЭлемента;

	НовыйЭлемент = ПолучитьОписаниеЭлемента(ИмяЭлемента, "ГруппаФормы", Группа);
	ПараметрыВыполнения.Элементы.Добавить(НовыйЭлемент);

	ДобавитьСвойство(НовыйЭлемент, "Вид", "ВидГруппыФормы", "ОбычнаяГруппа");
	ДобавитьСвойство(НовыйЭлемент, "Отображение", "ОтображениеОбычнойГруппы", "Нет");
	ДобавитьСвойство(НовыйЭлемент, "ОтображатьЗаголовок", "Булево", Ложь);
	ДобавитьСвойство(НовыйЭлемент, "Группировка", "ГруппировкаПодчиненныхЭлементовФормы", "ГоризонтальнаяВсегда");
	ДобавитьСвойство(НовыйЭлемент, "РастягиватьПоГоризонтали", "Булево", Истина);
	ДобавитьСвойство(НовыйЭлемент, "РастягиватьПоВертикали", "Булево", Ложь);

	ПостроитьЭлементы(ПараметрыВыполнения, ЭлементДанных, ИмяЭлемента);
	
	Выполнена = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьСтраницы(ПараметрыВыполнения, ЭлементДанных, Группа, Выполнена)  	
	Если ЭлементДанных.Тип <> "Страницы" Тогда 
		Возврат;
	КонецЕсли;
	
	СтрокаРеквизитовСтраницы = ПолучитьСтрокуТаблицыРеквизитов(
		ПараметрыВыполнения.ОписанияРеквизитов,
		ЭлементДанных.УИД);
	ИмяЭлемента = СтрокаРеквизитовСтраницы.ИмяЭлемента;
	
	НовыйЭлемент = ПолучитьОписаниеЭлемента(ИмяЭлемента, "ГруппаФормы", Группа);
	ПараметрыВыполнения.Элементы.Добавить(НовыйЭлемент);

	ДобавитьСвойство(НовыйЭлемент, "Вид", "ВидГруппыФормы", "Страницы");
	ДобавитьСвойство(НовыйЭлемент, "РастягиватьПоГоризонтали", "Булево", Истина);
	ДобавитьСвойство(НовыйЭлемент, "РастягиватьПоВертикали", "Булево", Ложь);
	
	Страницы = ЭлементДанных.Элементы;
	
	Для Каждого Страница Из Страницы Цикл
		ДобавитьСтраницу(ПараметрыВыполнения, ИмяЭлемента, Страница);
	КонецЦикла;
	
	Выполнена = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьСтраницу(ПараметрыВыполнения, ИмяСтраницы, Страница)  	  
	СтрокаРеквизитов = ПолучитьСтрокуТаблицыРеквизитов(
		ПараметрыВыполнения.ОписанияРеквизитов,
		Страница.УИД);
	ИмяЭлемента = СтрокаРеквизитов.ИмяЭлемента;
	
	НовыйЭлемент = ПолучитьОписаниеЭлемента(ИмяЭлемента, "ГруппаФормы", ИмяСтраницы);
	ПараметрыВыполнения.Элементы.Добавить(НовыйЭлемент);

	ДобавитьСвойство(НовыйЭлемент, "Вид", "ВидГруппыФормы", "Страница");
	ДобавитьСвойство(НовыйЭлемент, "Заголовок", "Строка", Страница.НаборСвойств.Заголовок);
		
	ПостроитьЭлементы(ПараметрыВыполнения, Страница, ИмяЭлемента);
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьЭлементПолеВвода(ПараметрыВыполнения, ЭлементДанных, Группа, Выполнена)     
	Если ЭлементДанных.Тип <> "ПолеВвода" Тогда 
		Возврат;
	КонецЕсли;

	СтрокаРеквизитов = ПолучитьСтрокуТаблицыРеквизитов(
		ПараметрыВыполнения.ОписанияРеквизитов,
		ЭлементДанных.УИД);
	ИмяЭлемента = СтрокаРеквизитов.ИмяЭлемента;
	ИмяРеквизита =  СтрокаРеквизитов.ИмяРеквизита;
	
	НовыйЭлемент = ПолучитьОписаниеЭлемента(ИмяЭлемента, "ПолеФормы", Группа);
	ПараметрыВыполнения.Элементы.Добавить(НовыйЭлемент);
	
	ДобавитьСвойство(НовыйЭлемент, "ПутьКДанным", "Строка", ИмяРеквизита);
	ДобавитьСвойство(НовыйЭлемент, "Вид", "ВидПоляФормы", "ПолеВвода");
	ДобавитьСвойство(НовыйЭлемент, "АвтоМаксимальнаяШирина", "Булево", Ложь);

	ЗначениеПриведенное = СлужебныеФункции().ПривестиЗначениеКДопустимомуТипу(ЭлементДанных.ОписаниеТипов, ЭлементДанных.Значение);
	Данные = Новый Структура("Тип,Значение", "ПолеФормы", ЗначениеПриведенное);
	ПараметрыВыполнения.Данные.Вставить(ИмяРеквизита, Данные);
	
	УстановитьСвойствоЭлементаНаСервере(НовыйЭлемент, ЭлементДанных);
	
	Если НЕ ЭлементДанных.НаборСвойств.Свойство("РастягиватьПоГоризонтали") Тогда
		ДобавитьСвойство(НовыйЭлемент, "РастягиватьПоГоризонтали", "Булево", Истина);
	КонецЕсли;
	
	Выполнена = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьЭлементНадпись(ПараметрыВыполнения, ЭлементДанных, Группа, Выполнена)  	
	Если ЭлементДанных.Тип <> "Надпись" Тогда 
		Возврат;
	КонецЕсли;
	
	СтрокаРеквизитов = ПолучитьСтрокуТаблицыРеквизитов(
		ПараметрыВыполнения.ОписанияРеквизитов,
		ЭлементДанных.УИД);
	ИмяЭлемента = СтрокаРеквизитов.ИмяЭлемента;

	НовыйЭлемент = ПолучитьОписаниеЭлемента(ИмяЭлемента, "ДекорацияФормы", Группа);
	ПараметрыВыполнения.Элементы.Добавить(НовыйЭлемент);
	
	ДобавитьСвойство(НовыйЭлемент, "Вид", "ВидДекорацииФормы", "Надпись");

	УстановитьСвойствоЭлементаНаСервере(НовыйЭлемент, ЭлементДанных);

	Выполнена = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьЭлементФлажок(ПараметрыВыполнения, ЭлементДанных, Группа, Выполнена)  
	Если ЭлементДанных.Тип <> "Флажок" Тогда 
		Возврат;
	КонецЕсли;
	
	СтрокаРеквизитов = ПолучитьСтрокуТаблицыРеквизитов(
		ПараметрыВыполнения.ОписанияРеквизитов,
		ЭлементДанных.УИД);
	ИмяЭлемента = СтрокаРеквизитов.ИмяЭлемента;
	ИмяРеквизита =  СтрокаРеквизитов.ИмяРеквизита;
	
	НовыйЭлемент = ПолучитьОписаниеЭлемента(ИмяЭлемента, "ПолеФормы", Группа);
	ПараметрыВыполнения.Элементы.Добавить(НовыйЭлемент);

	ДобавитьСвойство(НовыйЭлемент, "Вид", "ВидПоляФормы", "ПолеФлажка");
	ДобавитьСвойство(НовыйЭлемент, "ПутьКДанным", "Строка", ИмяРеквизита);

	УстановитьСвойствоЭлементаНаСервере(НовыйЭлемент, ЭлементДанных);
	
	Данные = Новый Структура("Тип,Значение", "ПолеФормы", ЭлементДанных.Значение);
	ПараметрыВыполнения.Данные.Вставить(ИмяРеквизита, Данные);
	
	Выполнена = Истина;
КонецПроцедуры

#Область ТаблицаДерево

&НаКлиенте
Процедура ДобавитьТаблицуДерево(ПараметрыВыполнения, ЭлементДанных, Группа, Выполнена)  
	Если ЭлементДанных.Тип <> "Дерево" 
		И ЭлементДанных.Тип <> "Таблица" Тогда 
		Возврат;
	КонецЕсли;

	ОписанияРеквизитов = ПараметрыВыполнения.ОписанияРеквизитов;
	
	СтрокаРеквизитовТаблица = ПолучитьСтрокуТаблицыРеквизитов(ОписанияРеквизитов, ЭлементДанных.УИД);
	ИмяЭлемента = СтрокаРеквизитовТаблица.ИмяЭлемента;
	ИмяРеквизита = СтрокаРеквизитовТаблица.ИмяРеквизита;
	
	НовыйЭлемент = ПолучитьОписаниеЭлемента(ИмяЭлемента, "ТаблицаФормы", Группа);
	ПараметрыВыполнения.Элементы.Добавить(НовыйЭлемент);
	
	ДобавитьСвойство(НовыйЭлемент, "ПутьКДанным", "Строка", ИмяРеквизита);
	ДобавитьСвойство(НовыйЭлемент, "ПоложениеКоманднойПанели", "ПоложениеКоманднойПанелиЭлементаФормы", "Нет");
	
	Колонки = ЭлементДанных.Колонки;
	Строки = ЭлементДанных.Строки; 
	
	СоответствиеКолонок = Новый Соответствие;
	
	Для Каждого Колонка Из Колонки Цикл
		ДобавитьКолонкуТаблицыРекурсивно(ПараметрыВыполнения, ИмяЭлемента, "Таблица", ИмяРеквизита, Колонка, СоответствиеКолонок);
	КонецЦикла;          
	
	Данные = Новый Структура("Тип,Строки", "Таблица", Новый Массив);
	ПараметрыВыполнения.Данные.Вставить(ИмяРеквизита, Данные);
	Если ЭлементДанных.Тип = "Дерево"  Тогда
		Данные.Тип = "Дерево";
		ДобавитьДеревоСтроки(ПараметрыВыполнения, Строки, СоответствиеКолонок, Данные, Истина);
	Иначе
		ДобавитьДеревоСтроки(ПараметрыВыполнения, Строки, СоответствиеКолонок, Данные, Ложь);
	КонецЕсли;
	
	Выполнена = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьГруппуКолонокТаблицы(ПараметрыВыполнения, ИмяЭлементаРодителя, ИмяЭлемента, ГруппировкаКолонок, Заголовок = Неопределено)
	НоваяКолонка = ПолучитьОписаниеЭлемента(ИмяЭлемента, "ГруппаФормы", ИмяЭлементаРодителя);
	ПараметрыВыполнения.Элементы.Добавить(НоваяКолонка);

	ДобавитьСвойство(НоваяКолонка, "Вид", "ВидГруппыФормы", "ГруппаКолонок");
	ДобавитьСвойство(НоваяКолонка, "Группировка", "ГруппировкаКолонок", ГруппировкаКолонок);
	
	Если Заголовок <> Неопределено Тогда  
		ДобавитьСвойство(НоваяКолонка, "Заголовок", "Строка", Заголовок);
		ДобавитьСвойство(НоваяКолонка, "ОтображатьВШапке", "Булево", Истина);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьКолонкуТаблицыРекурсивно(ПараметрыВыполнения, ИмяЭлементаРодителя, ТипРодителя, ИмяРеквизитаТаблица, Колонка, СоответствиеКолонок)
	ОписанияРеквизитов = ПараметрыВыполнения.ОписанияРеквизитов;
	
	СтрокаРеквизитов = ПолучитьСтрокуТаблицыРеквизитов(ОписанияРеквизитов, Колонка.УИД);
	
	ТекИмяЭлементаРодителя = ИмяЭлементаРодителя;
	ТекТипРодителя = ТипРодителя;
	
	Колонки = Колонка.Колонки;
	КоличествоКолонок = Колонка.Колонки.Количество();
	
	ЕстьПодчиненные = КоличествоКолонок > 0;
	ЕстьГруппаПодчиненных = КоличествоКолонок > 1;
	
	ТребуетсяДобавитьГруппуКолонок = 
		Колонка.Тип = "ГруппаКолонокТаблицы";
		
	ТребуетсяДобавитьВертикальнуюГруппу = 
		НЕ ТребуетсяДобавитьГруппуКолонок 
		И ЕстьПодчиненные 
		И (ТипРодителя = "ГоризонтальнаяГруппа" 
			ИЛИ ТипРодителя = "Таблица");
			
	ТребуетсяДобавитьГоризонтальнуюГруппу =
		ТребуетсяДобавитьГруппуКолонок ИЛИ
		ЕстьГруппаПодчиненных;
			
	ТребуетсяДобавитьКолонкуТаблицы =
		НЕ ТребуетсяДобавитьГруппуКолонок;
		
	ЗаголовокГруппы = Неопределено;
	Если ТребуетсяДобавитьГруппуКолонок Тогда
		ЗаголовокГруппы = Колонка.НаборСвойств.Заголовок;
	КонецЕсли;
	
	Если ТребуетсяДобавитьВертикальнуюГруппу Тогда
		
		ИмяГруппы = ИмяРеквизитаТаблица + СтрокаРеквизитов.ИмяЭлемента + "Вертикальная";
		
		ДобавитьГруппуКолонокТаблицы(ПараметрыВыполнения, ТекИмяЭлементаРодителя, ИмяГруппы, "Вертикальная");
		ТекИмяЭлементаРодителя = ИмяГруппы;
		ТекТипРодителя = "ВертикальнаяГруппа";
	КонецЕсли;
	
	Если ТребуетсяДобавитьКолонкуТаблицы Тогда
		ДобавитьКолонкуТаблицы(ПараметрыВыполнения, ОписанияРеквизитов, ТекИмяЭлементаРодителя, ИмяРеквизитаТаблица, Колонка, СоответствиеКолонок);
	КонецЕсли;
	
	Если ТребуетсяДобавитьГоризонтальнуюГруппу Тогда
		ИмяГруппы = ИмяРеквизитаТаблица + СтрокаРеквизитов.ИмяЭлемента + "Горизонтальная";
		
		ДобавитьГруппуКолонокТаблицы(
			ПараметрыВыполнения,
			ТекИмяЭлементаРодителя, 
			ИмяГруппы, 
			"Горизонтальная",
			ЗаголовокГруппы);
		ТекИмяЭлементаРодителя = ИмяГруппы;
		ТекТипРодителя = "ГоризонтальнаяГруппа";
	КонецЕсли;

	Для Каждого Колонка Из Колонки Цикл
		ДобавитьКолонкуТаблицыРекурсивно(ПараметрыВыполнения, ТекИмяЭлементаРодителя, ТекТипРодителя, ИмяРеквизитаТаблица, Колонка, СоответствиеКолонок);
	КонецЦикла;          
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьКолонкуТаблицы(ПараметрыВыполнения, ОписанияРеквизитов,Знач ЭлементРодитель, ИмяРеквизитаТаблица, Колонка, СоответствиеКолонок)
	ТекРодитель = ЭлементРодитель;
	
	ЕстьГруппа = Колонка.ЕстьФлажок И Колонка.ЕстьЗначение;                

	Если ЕстьГруппа Тогда
		СтрокаРеквизитов = ПолучитьСтрокуТаблицыРеквизитов(ОписанияРеквизитов, Колонка.УИД);
		ИмяЭлемента = ИмяРеквизитаТаблица + СтрокаРеквизитов.ИмяЭлемента + "Вместе";
		
		ДобавитьГруппуКолонокТаблицы(
		    ПараметрыВыполнения,
			ТекРодитель, 
			ИмяЭлемента, 
			"ВЯчейке", 
			Колонка.НаборСвойств.Заголовок);
		ТекРодитель = ИмяЭлемента;
	КонецЕсли;
	
	Если Колонка.ЕстьФлажок Тогда
		СтрокаРеквизитов = ПолучитьСтрокуТаблицыРеквизитов(ОписанияРеквизитов, Колонка.УИДФлажок);
		ИмяЭлемента = ИмяРеквизитаТаблица + СтрокаРеквизитов.ИмяЭлемента;
		ИмяРеквизита = ИмяРеквизитаТаблица + "." + СтрокаРеквизитов.ИмяРеквизита;
		
		НоваяКолонка = ПолучитьОписаниеЭлемента(ИмяЭлемента, "ПолеФормы", ТекРодитель);
		ПараметрыВыполнения.Элементы.Добавить(НоваяКолонка);
	
		ДобавитьСвойство(НоваяКолонка, "Вид", "ВидПоляФормы", "ПолеФлажка");
		ДобавитьСвойство(НоваяКолонка, "ПутьКДанным", "Строка", ИмяРеквизита);
		Если ЕстьГруппа Тогда
			ДобавитьСвойство(НоваяКолонка, "ОтображатьВШапке", "Булево", Ложь);
		КонецЕсли;
		
		ДобавитьСвойство(НоваяКолонка, "Заголовок", "Строка", Колонка.НаборСвойств.Заголовок);
		
	КонецЕсли;
	
	Если Колонка.ЕстьЗначение Тогда
		СоответствиеКолонок.Вставить(Колонка.УИД, Колонка);
		
		СтрокаРеквизитов = ПолучитьСтрокуТаблицыРеквизитов(ОписанияРеквизитов, Колонка.УИД);
		ИмяЭлемента = ИмяРеквизитаТаблица + СтрокаРеквизитов.ИмяЭлемента;
		ИмяРеквизита = ИмяРеквизитаТаблица + "." + СтрокаРеквизитов.ИмяРеквизита;	
		
		НоваяКолонка = ПолучитьОписаниеЭлемента(ИмяЭлемента, "ПолеФормы", ТекРодитель);
		ПараметрыВыполнения.Элементы.Добавить(НоваяКолонка);
	
		ДобавитьСвойство(НоваяКолонка, "Вид", "ВидПоляФормы", "ПолеВвода");
		ДобавитьСвойство(НоваяКолонка, "ПутьКДанным", "Строка", ИмяРеквизита);
		
		УстановитьСвойствоЭлементаНаСервере(НоваяКолонка, Колонка);
		
		Если ЕстьГруппа Тогда
			ДобавитьСвойство(НоваяКолонка, "ОтображатьВШапке", "Булево", Ложь);
		КонецЕсли;

		ДобавитьСвойство(НоваяКолонка, "Заголовок", "Строка", Колонка.НаборСвойств.Заголовок);
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ДобавитьДеревоСтроки(ПараметрыВыполнения, Строки, СоответствиеКолонок, ИсходящиеДанные, ЭтоДерево)
	Для Каждого Строка Из Строки Цикл 
		ОписаниеСтроки = Новый Структура("Строки,Значения", Новый Массив, Новый Структура);
		ИсходящиеДанные.Строки.Добавить(ОписаниеСтроки);
		
		Для Каждого Ячейка Из Строка.Ячейки Цикл  
			Если НЕ ПустаяСтрока(Ячейка.Значение) Тогда
				СтрокаРеквизитов = ПолучитьСтрокуТаблицыРеквизитов(
					ПараметрыВыполнения.ОписанияРеквизитов, Ячейка.УИДКолонки);   
					
				Колонка = СоответствиеКолонок.Получить(Ячейка.УИДКолонки);
				ЗначениеПриведенное = СлужебныеФункции().ПривестиЗначениеКДопустимомуТипу(Колонка.ОписаниеТипов, Ячейка.Значение);

				ОписаниеСтроки.Значения.Вставить(СтрокаРеквизитов.ИмяРеквизита, ЗначениеПриведенное);
			КонецЕсли;
			
			Если Ячейка.ЕстьФлажок Тогда
				СтрокаРеквизитовФлажок = ПолучитьСтрокуТаблицыРеквизитов(
					ПараметрыВыполнения.ОписанияРеквизитов,
					Ячейка.УИДКолонкиФлажок);
				ОписаниеСтроки.Значения.Вставить(СтрокаРеквизитовФлажок.ИмяРеквизита, Ячейка.ЗначениеФлажка);
			КонецЕсли;
		КонецЦикла;
		
		Если ЭтоДерево Тогда
			ДобавитьДеревоСтроки(ПараметрыВыполнения, Строка.Строки, СоответствиеКолонок, ОписаниеСтроки, Истина);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область Кнопки

&НаКлиенте
Процедура ДобавитьКоманднуюПанель(ПараметрыВыполнения, ЭлементДанных, ИмяГруппы, Выполнена)  
	Если ЭлементДанных.Тип <> "КоманднаяПанель" Тогда 
		Возврат;
	КонецЕсли;
	
	Если ДобавитьЕдинственнуюКнопку(ПараметрыВыполнения, ЭлементДанных, ИмяГруппы) Тогда
		Выполнена = Истина;
		Возврат;
	КонецЕсли;
	
	СтрокаРеквизитовКоманднаяПанель = ПолучитьСтрокуТаблицыРеквизитов(
		ПараметрыВыполнения.ОписанияРеквизитов, 
		ЭлементДанных.УИД);
	ИмяЭлемента = СтрокаРеквизитовКоманднаяПанель.ИмяЭлемента;
	
	НовыйЭлемент = ПолучитьОписаниеЭлемента(ИмяЭлемента, "ГруппаФормы", ИмяГруппы);
	ПараметрыВыполнения.Элементы.Добавить(НовыйЭлемент);
	
	ДобавитьСвойство(НовыйЭлемент, "Вид", "ВидГруппыФормы", "КоманднаяПанель");

	УстановитьСвойствоЭлементаНаСервере(НовыйЭлемент, ЭлементДанных);
	
	Если НЕ ЭлементДанных.НаборСвойств.Свойство("РастягиватьПоГоризонтали") Тогда
		ДобавитьСвойство(НовыйЭлемент, "РастягиватьПоГоризонтали", "Булево", Ложь);
	КонецЕсли;
	
	ДобавитьКнопки(ПараметрыВыполнения, ЭлементДанных, ИмяЭлемента, ИмяЭлемента);
	
	Выполнена = Истина;
КонецПроцедуры

&НаКлиенте
Функция ДобавитьЕдинственнуюКнопку(ПараметрыВыполнения, ЭлементДанных, Группа)
	Если ЭлементДанных.Элементы.Количество() <> 1 Тогда
		Возврат Ложь;
	КонецЕсли;  
	
	Кнопка = ЭлементДанных.Элементы[0];
	Если Кнопка.Тип <> "Кнопка" Тогда
		Возврат Ложь;
	КонецЕсли;

	ДобавитьКнопку(
		ПараметрыВыполнения,
		Кнопка, 
		Группа, 
		"");
	Возврат Истина;
КонецФункции

&НаКлиенте
Процедура ДобавитьКнопки(ПараметрыВыполнения, ЭлементДанных, ЭлементФормыРодитель, ИмяЭлементаКоманднаяПанель)  
	Для Каждого Кнопка Из ЭлементДанных.Элементы Цикл   
		Если Кнопка.Тип = "ГруппаКнопок" Тогда
			ДобавитьГруппуКнопок(
				ПараметрыВыполнения,
				Кнопка, 
				ЭлементФормыРодитель, 
				ИмяЭлементаКоманднаяПанель);
			Продолжить;
		КонецЕсли;
		
		Если Кнопка.Тип = "Меню" Тогда
			ДобавитьМеню(
				ПараметрыВыполнения,
				Кнопка, 
				ЭлементФормыРодитель, 
				ИмяЭлементаКоманднаяПанель);
			Продолжить;
		КонецЕсли;
		ДобавитьКнопку(
			ПараметрыВыполнения,
			Кнопка, 
			ЭлементФормыРодитель, 
			ИмяЭлементаКоманднаяПанель);
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьГруппуКнопок(ПараметрыВыполнения, Кнопка, ИмяРодителя, ИмяКоманднойПанели)  
	СтрокаРеквизитов = ПолучитьСтрокуТаблицыРеквизитов(ПараметрыВыполнения.ОписанияРеквизитов, Кнопка.УИД);
	ИмяЭлемента = СтрокаРеквизитов.ИмяЭлемента;

	НовыйЭлемент = ПолучитьОписаниеЭлемента(ИмяЭлемента, "ГруппаФормы", ИмяРодителя);
	ПараметрыВыполнения.Элементы.Добавить(НовыйЭлемент);
	
	ДобавитьСвойство(НовыйЭлемент, "Вид", "ВидГруппыФормы", "ГруппаКнопок");
	
	УстановитьСвойствоЭлементаНаСервере(НовыйЭлемент, Кнопка);
	
	ДобавитьКнопки(ПараметрыВыполнения, Кнопка, ИмяЭлемента, ИмяКоманднойПанели);
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьМеню(ПараметрыВыполнения, Кнопка, ЭлементФормыРодитель, ИмяКоманднойПанели)  
	СтрокаРеквизитов = ПолучитьСтрокуТаблицыРеквизитов(ПараметрыВыполнения.ОписанияРеквизитов, Кнопка.УИД);
	ИмяЭлемента = СтрокаРеквизитов.ИмяЭлемента;

	НовыйЭлемент = ПолучитьОписаниеЭлемента(ИмяЭлемента, "ГруппаФормы", ЭлементФормыРодитель);
	ПараметрыВыполнения.Элементы.Добавить(НовыйЭлемент);
	
	ДобавитьСвойство(НовыйЭлемент, "Вид", "ВидГруппыФормы", "Подменю");
	ДобавитьСвойство(НовыйЭлемент, "Заголовок", "Строка", Кнопка.НаборСвойств.Заголовок);
	
	УстановитьСвойствоЭлементаНаСервере(НовыйЭлемент, Кнопка);
	
	Если Кнопка.Картинка <> Неопределено Тогда
		Если ПустаяСтрока(Кнопка.НаборСвойств.Заголовок) Тогда
			ДобавитьСвойство(НовыйЭлемент, "Отображение", "ОтображениеКнопки", "Картинка");
		Иначе
			ДобавитьСвойство(НовыйЭлемент, "Отображение", "ОтображениеКнопки", "КартинкаИТекст");
		КонецЕсли;
	КонецЕсли;
	
	Если Кнопка.Картинка <> Неопределено Тогда
		ДобавитьСвойство(НовыйЭлемент, "Картинка", "БиблиотекаКартинок", Кнопка.Картинка);
	КонецЕсли;
	
	ДобавитьКнопки(ПараметрыВыполнения, Кнопка, ИмяЭлемента, ИмяКоманднойПанели);
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьКнопку(ПараметрыВыполнения, Кнопка, ЭлементФормыРодитель, ИмяЭлементаКоманднаяПанель)  
	СтрокаРеквизитов = ПолучитьСтрокуТаблицыРеквизитов(ПараметрыВыполнения.ОписанияРеквизитов, Кнопка.УИД);
	ИмяЭлемента = СтрокаРеквизитов.ИмяЭлемента;

	НовыйЭлемент = ПолучитьОписаниеЭлемента(ИмяЭлемента, "КнопкаФормы", ЭлементФормыРодитель);
	ПараметрыВыполнения.Элементы.Добавить(НовыйЭлемент);
	
	ДобавитьСвойство(НовыйЭлемент, "ИмяКоманды", "Строка", "__КомандаЗаглушка");
	ДобавитьСвойство(НовыйЭлемент, "Заголовок", "Строка", Кнопка.НаборСвойств.Заголовок);
       
	УстановитьСвойствоЭлементаНаСервере(НовыйЭлемент, Кнопка);

	Если Кнопка.Картинка <> Неопределено Тогда
		Если ПустаяСтрока(Кнопка.НаборСвойств.Заголовок) Тогда
			ДобавитьСвойство(НовыйЭлемент, "Отображение", "ОтображениеКнопки", "Картинка");
		Иначе
			ДобавитьСвойство(НовыйЭлемент, "Отображение", "ОтображениеКнопки", "КартинкаИТекст");
		КонецЕсли;
	КонецЕсли;
	
	Если Кнопка.Картинка <> Неопределено Тогда 
		ДобавитьСвойство(НовыйЭлемент, "Картинка", "БиблиотекаКартинок", Кнопка.Картинка);
	КонецЕсли;   	
КонецПроцедуры

#КонецОбласти 

&НаКлиенте
Процедура УстановитьСвойствоЭлементаНаСервере(ЭлементФормы, ЭлементДанных)
	Для Каждого КлючЗначение Из ЭлементДанных.НаборСвойств Цикл
		ИмяСвойства = КлючЗначение.Ключ;
		СтрЗначениеСвойства = КлючЗначение.Значение;
		
		Если НЕ ЭлементДанных.ТипыСвойств.Свойство(ИмяСвойства) Тогда
			Продолжить;
		КонецЕсли;
		
		ТипСвойства = ЭлементДанных.ТипыСвойств[ИмяСвойства]; 
		
		СтрЗначениеСвойства = ПолучитьТипЗначениеСвойства(СтрЗначениеСвойства, ТипСвойства);
		
		ДобавитьСвойство(ЭлементФормы, ИмяСвойства, СтрЗначениеСвойства.Тип, СтрЗначениеСвойства.Значение); 
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Функция ПолучитьТипЗначениеСвойства(Значение, Тип)
	Результат = Новый Структура("Тип,Значение", Тип, Значение);
	
	Если Тип = "Булево" Тогда
		Результат.Значение = Булево(Значение);
	КонецЕсли;

	Если Тип = "Число" Тогда
		Результат.Значение = Число(Значение);
	КонецЕсли;

	Если Тип = "Цвет" Тогда
		Результат.Тип = "WebЦвета";
	КонецЕсли;
	
	Возврат Результат;
КонецФункции 

&НаКлиенте
Функция ПолучитьСтрокуТаблицыРеквизитов(ОписанияРеквизитов, УИД)
	Результат = Новый Структура("ИмяРеквизита,ИмяЭлемента");

	ТекущееОписание = Неопределено;
	Для Каждого Описание Из ОписанияРеквизитов Цикл
		Если Описание.УИД <> УИД Тогда
			Продолжить;
		КонецЕсли;
		
		ТекущееОписание = Описание;
		Прервать;
	КонецЦикла;
	Результат.ИмяРеквизита = ТекущееОписание.ИмяРеквизита;
	Результат.ИмяЭлемента = ТекущееОписание.ИмяЭлемента;
	
	Если ТекущееОписание.Номер > 1 Тогда
		СтрокаНомер = Формат(ТекущееОписание.Номер - 1, "ЧГ=0");
		Результат.ИмяРеквизита = Результат.ИмяРеквизита + СтрокаНомер;
		Результат.ИмяЭлемента = Результат.ИмяЭлемента + СтрокаНомер;
	КонецЕсли;
	
	Возврат Результат;	
КонецФункции

#КонецОбласти 
