// MIT License

// Copyright (c) 2025 Zherebtsov Nikita <nikita@crimsongold.ru>

// Permission is hereby granted, free of charge, to any person obtaining a copy
// of this software and associated documentation files (the "Software"), to deal
// in the Software without restriction, including without limitation the rights
// to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
// copies of the Software, and to permit persons to whom the Software is
// furnished to do so, subject to the following conditions:

// The above copyright notice and this permission notice shall be included in all
// copies or substantial portions of the Software.

// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
// IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
// FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
// AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
// LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
// OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
// SOFTWARE.

// https://github.com/crimsongoldteam/md_design


#Область ОписаниеПеременных

// Структура, хранящие в себе инициализированные модули
&НаКлиенте
Перем мВсеСвойства;

// Структура, хранящие соответстие русских и английских названий элементов
&НаКлиенте
Перем мТипыЭлементов;

#КонецОбласти 

#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура Инициализировать() Экспорт 
	Данные = ПолучитьДанныеНаСервере();
	мВсеСвойства =  Данные.ВсеСвойства; 
	мТипыЭлементов = Данные.ТипыЭлементов;
КонецПроцедуры

&НаКлиенте
Функция ПолучитьСвойстваЭлементаДляПалитры(ЭлементАСД) Экспорт
	Если ЭлементАСД = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если НЕ мВсеСвойства.Свойство(ЭлементАСД.Тип) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Результат = Новый Массив;
	
	СвойстваПоТипу = мВсеСвойства[ЭлементАСД.Тип]; 
	Для Каждого КлючЗначение Из СвойстваПоТипу Цикл
		ТекСвойство = Новый Структура("ПредставлениеПоля,ИмяПоля,ЗначениеПоля,ЗначениеПоУмолчанию,Категория,Изменено");
		ТекСвойство.ПредставлениеПоля = КлючЗначение.Значение.Представление;
		ТекСвойство.ИмяПоля = КлючЗначение.Ключ;
		ТекСвойство.ЗначениеПоУмолчанию = КлючЗначение.Значение.ЗначениеПоУмолчанию;
		
		Если КлючЗначение.Значение.ТипЗначения = "Булево" Тогда
			ТекСвойство.Категория = "Булево"; 
		ИначеЕсли КлючЗначение.Значение.ТипЗначения = "Цвет" Тогда
			ТекСвойство.Категория = "Цвет"; 
		ИначеЕсли КлючЗначение.Значение.ТипЗначения = "Число" Тогда
			ТекСвойство.Категория = "Число"; 
		ИначеЕсли КлючЗначение.Значение.ТипЗначения = "Строка" Тогда
			ТекСвойство.Категория = "Строка"; 
		Иначе
			ТекСвойство.Категория = "Список"; 
		КонецЕсли;
		
		Если ЭлементАСД.НаборСвойств.Свойство(КлючЗначение.Ключ) Тогда
			ТекСвойство.ЗначениеПоля = ЭлементАСД.НаборСвойств[КлючЗначение.Ключ];
		Иначе
			ТекСвойство.ЗначениеПоля = ТекСвойство.ЗначениеПоУмолчанию;
		КонецЕсли;
		
		ТекСвойство.Изменено = ТекСвойство.ЗначениеПоля <> ТекСвойство.ЗначениеПоУмолчанию;

		
		Результат.Добавить(ТекСвойство);
	КонецЦикла;

	Возврат Результат;
КонецФункции     

&НаКлиенте
Процедура ЗаполнитьВсеСвойстваДляПалитры(ТаблицаПалитра) Экспорт 
	Для Каждого КЗТипЭлемента Из мВсеСвойства Цикл  
		ТипЭлемента = КЗТипЭлемента.Ключ;
		Для Каждого КЗСвойство Из КЗТипЭлемента.Значение Цикл   
			Если КЗСвойство.Ключ = "ПутьКДанным" Тогда
				Продолжить;
			КонецЕсли;
			ЗначениеСвойства = КЗСвойство.Значение;
			
			СтрокаТаблица = ТаблицаПалитра.Добавить();                        
			СтрокаТаблица.ТипЭлемента = ТипЭлемента;
			СтрокаТаблица.ИмяПоля = КЗСвойство.Ключ;
			СтрокаТаблица.ПредставлениеПоля = ЗначениеСвойства.Представление;
			СтрокаТаблица.ЗначениеПоУмолчанию = ЗначениеСвойства.ЗначениеПоУмолчанию;
			
			Если ЗначениеСвойства.ТипЗначения = "Булево" Тогда
				СтрокаТаблица.Категория = "Булево"; 
			ИначеЕсли ЗначениеСвойства.ТипЗначения = "Цвет" Тогда
				СтрокаТаблица.Категория = "Цвет";              
				НовыйЦвет = Новый Цвет;                      
				СтрокаТаблица.ЗначениеПоУмолчанию = НовыйЦвет;
			ИначеЕсли ЗначениеСвойства.ТипЗначения = "Число" Тогда
				СтрокаТаблица.Категория = "Число"; 
			ИначеЕсли ЗначениеСвойства.ТипЗначения = "Строка" Тогда
				СтрокаТаблица.Категория = "Строка"; 
			Иначе
				СтрокаТаблица.Категория = "Список"; 
			КонецЕсли;
			
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Функция ПолучитьСписокСвойствПоТипу(ТипЭлемента, ИмяСвойства) Экспорт    
	Результат = Новый Массив;
	Если НЕ мВсеСвойства.Свойство(ТипЭлемента) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	СтрСвойство = мВсеСвойства[ТипЭлемента][ИмяСвойства];	
	
	Для Каждого ТекЗначение Из СтрСвойство.Значения Цикл
		Результат.Добавить(Новый Структура("Ключ,Значение", ТекЗначение, СтрСвойство.ПредставленияЗначений[ТекЗначение]));
	КонецЦикла;
	
	Возврат Результат;
КонецФункции

&НаКлиенте
Функция ПолучитьАнглийскоеИмяТипаЭлемента(ТипЭлемента) Экспорт
	Если НЕ мТипыЭлементов.Свойство(ТипЭлемента) Тогда
		Возврат Неопределено;
	КонецЕсли;
	Возврат мТипыЭлементов[ТипЭлемента];
КонецФункции

&НаКлиенте
Функция ПолучитьАнглийскоеИмяСвойства(ТипЭлемента, ИмяСвойства) Экспорт   
	Если НЕ мВсеСвойства.Свойство(ТипЭлемента) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	СвойстваПоТипу = мВсеСвойства[ТипЭлемента]; 
	
	Если НЕ СвойстваПоТипу.Свойство(ИмяСвойства) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат СвойстваПоТипу[ИмяСвойства].ИмяАнгл;
КонецФункции

&НаКлиенте
Функция ПолучитьАнглийскоеЗначениеСвойства(ТипЭлемента, ИмяСвойства, ЗначениеСвойства) Экспорт
	Если НЕ мВсеСвойства.Свойство(ТипЭлемента) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	СвойстваПоТипу = мВсеСвойства[ТипЭлемента];
	
	Если НЕ СвойстваПоТипу.Свойство(ИмяСвойства) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	СтрСвойство = СвойстваПоТипу[ИмяСвойства];
	
	Если НЕ СтрСвойство.ЗначенияАнгл.Свойство(ЗначениеСвойства) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат СтрСвойство.ЗначенияАнгл[ЗначениеСвойства];
КонецФункции

&НаКлиенте
Процедура ЗаполнитьНеизвестныеСвойства(ЭлементАСД) Экспорт
	ЗаполнитьТипыСвойств(ЭлементАСД);
	
	МассивИерархий = СтрРазделить("Элементы,Колонки,Ячейки,Строки", ",");
	Для Каждого Иерархия Из МассивИерархий Цикл
		Если НЕ ЭлементАСД.Свойство(Иерархия) Тогда
			Продолжить;
		КонецЕсли;
		
		Для Каждого ОписаниеЭлемента Из ЭлементАСД[Иерархия] Цикл
			ЗаполнитьНеизвестныеСвойства(ОписаниеЭлемента);
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗаполнитьТипыСвойств(ЭлементАСД)   
	Если НЕ ЭлементАСД.Свойство("НаборСвойств") Тогда
		Возврат;
	КонецЕсли;
	
	ЕстьСвойствоПоТипу = мВсеСвойства.Свойство(ЭлементАСД.Тип);
	Если ЕстьСвойствоПоТипу Тогда
		ВсеСвойстваПоТипу = мВсеСвойства[ЭлементАСД.Тип];
	КонецЕсли;
	
	Для Каждого КлючЗначение Из ЭлементАСД.НаборСвойств Цикл 
		ИмяСвойства = КлючЗначение.Ключ;
		ЗначениеСвойства = КлючЗначение.Значение;
		Если НЕ ЕстьСвойствоПоТипу Тогда
			Продолжить;
		КонецЕсли;
		
		Если НЕ ВсеСвойстваПоТипу.Свойство(ИмяСвойства) Тогда
			Продолжить;
		КонецЕсли;
		
		ОписаниеСвойства = ВсеСвойстваПоТипу[ИмяСвойства];
		
		Если НЕ ЭтоКорректноеЗначение(ЗначениеСвойства, ОписаниеСвойства) Тогда
			Продолжить;
		КонецЕсли;
		
		ЭлементАСД.ТипыСвойств.Вставить(ИмяСвойства, ОписаниеСвойства.ТипЗначения);
	КонецЦикла;	
КонецПроцедуры

&НаКлиенте
Функция ЭтоКорректноеЗначение(Значение, ОписаниеСвойства)
	Если ОписаниеСвойства.ТипЗначения = "Строка" Тогда
		Возврат Истина;
	КонецЕсли;

	Если ОписаниеСвойства.ТипЗначения = "Цвет" Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если ОписаниеСвойства.ТипЗначения = "Число" Тогда
		
		Возврат ЭтоЧисло(Значение);
	КонецЕсли;
	
	Если ОписаниеСвойства.ТипЗначения = "Булево" Тогда
		
		Возврат ЭтоБулево(Значение);
	КонецЕсли;
	
	Возврат ОписаниеСвойства.Значения.Найти(НРег(Значение)) <> Неопределено;
КонецФункции

&НаКлиенте
Функция ЭтоЧисло(Знач Значение) 
	Если Значение = "0" Тогда
		Возврат Истина;
	КонецЕсли;
	
	ОписаниеЧисла = Новый ОписаниеТипов("Число");
	
	Возврат ОписаниеЧисла.ПривестиЗначение(Значение) <> 0;
КонецФункции 

&НаКлиенте
Функция ЭтоБулево(Знач Значение)
	ДопустимыеЗначения = СтрРазделить("да,нет,0,1,истина,ложь", ",");
	
	Возврат ДопустимыеЗначения.Найти(НРег(Значение)) <> Неопределено;
КонецФункции

&НаКлиенте
Функция ПолучитьЭлементПоУИД(ОписаниеЭлементов, УИД) Экспорт
	Если ОписаниеЭлементов.УИД = УИД Тогда
		Возврат ОписаниеЭлементов;
	КонецЕсли;
	
	Если ОписаниеЭлементов.Свойство("Элементы") Тогда
		Для Каждого ОписаниеЭлемента Из ОписаниеЭлементов.Элементы Цикл
			ТекЭлемент = ПолучитьЭлементПоУИД(ОписаниеЭлемента, УИД);
			Если ТекЭлемент <> Неопределено Тогда
				Возврат ТекЭлемент;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если ОписаниеЭлементов.Свойство("Колонки") Тогда
		Для Каждого ОписаниеЭлемента Из ОписаниеЭлементов.Колонки Цикл
			ТекЭлемент = ПолучитьЭлементПоУИД(ОписаниеЭлемента, УИД);
			Если ТекЭлемент <> Неопределено Тогда
				Возврат ТекЭлемент;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Возврат Неопределено;
КонецФункции

&НаСервере
Функция ПолучитьДанныеНаСервере()
	Результат = Новый Структура;	
	Результат.Вставить("ВсеСвойства", ПолучитьСвойстваНаСервере());
	Результат.Вставить("ТипыЭлементов", ПолучитьТипыЭлементовНаСервере());
	Возврат Результат;
КонецФункции

&НаСервере
Функция ПолучитьСвойстваНаСервере()  
	МаксимальноеКоличествоЗначений = 6;
	
	Результат = Новый Структура;
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	ТабличныйДокумент = ОбработкаОбъект.ПолучитьМакет("Свойства");

	Построитель = Новый ПостроительЗапроса;
	Построитель.ИсточникДанных = Новый ОписаниеИсточникаДанных(ТабличныйДокумент.Область());  	
	
	ТаблицаЗначений = Построитель.Результат.Выгрузить();	
	
	Для Каждого СтрокаТаблицы Из ТаблицаЗначений Цикл
		Если Результат.Свойство(СтрокаТаблицы.ТипЭлемента) Тогда
			СвойстваПоТипуЭлемента = Результат[СтрокаТаблицы.ТипЭлемента];
		Иначе
			СвойстваПоТипуЭлемента = Новый Структура;
			Результат.Вставить(СтрокаТаблицы.ТипЭлемента, СвойстваПоТипуЭлемента);
		КонецЕсли;
		
		ОписаниеСвойства = ПолучитьСтруктуруСвойства();;
		
		ЗаполнитьЗначенияСвойств(ОписаниеСвойства, СтрокаТаблицы, "Представление,ТипЗначения,ИмяАнгл,ЗначениеПоУмолчанию");
		Если ОписаниеСвойства.ТипЗначения = "Булево" Тогда                                    
			Если ПустаяСтрока(ОписаниеСвойства.ЗначениеПоУмолчанию) Тогда
				ОписаниеСвойства.ЗначениеПоУмолчанию = Ложь;
			Иначе
				ОписаниеСвойства.ЗначениеПоУмолчанию = Булево(ОписаниеСвойства.ЗначениеПоУмолчанию);
			КонецЕсли;
			
		КонецЕсли;
		
		Для Сч = 1 По МаксимальноеКоличествоЗначений Цикл
			ТекущееЗначение = СтрокаТаблицы["Значение" + Строка(Сч)];
			Если ПустаяСтрока(ТекущееЗначение) Тогда
				Прервать;
			КонецЕсли;
			ОписаниеСвойства.Значения.Добавить(ТекущееЗначение);
			ОписаниеСвойства.ЗначенияАнгл.Вставить(ТекущееЗначение, СтрокаТаблицы["ЗначениеАнгл" + Строка(Сч)]);
			
			ОписаниеСвойства.ПредставленияЗначений.Вставить(ТекущееЗначение, СтрокаТаблицы["ПредставлениеЗначения" + Строка(Сч)]);
		КонецЦикла;
		
		СвойстваПоТипуЭлемента.Вставить(СтрокаТаблицы.Имя, ОписаниеСвойства); 
	КонецЦикла;
	
	Возврат Результат;
КонецФункции

&НаСервере
Функция ПолучитьСтруктуруСвойства()
	Результат = Новый Структура();
	Результат.Вставить("Представление", "");
	Результат.Вставить("ТипЗначения", "");
	Результат.Вставить("Значения", Новый Массив);
	Результат.Вставить("ПредставленияЗначений", Новый Структура);
	Результат.Вставить("ИмяАнгл", "");
	Результат.Вставить("ЗначениеПоУмолчанию", Неопределено);
	Результат.Вставить("ЗначенияАнгл", Новый Структура);
	
	Возврат Результат;
КонецФункции

&НаСервере
Функция ПолучитьТипыЭлементовНаСервере()  
	Результат = Новый Структура;
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	ТабличныйДокумент = ОбработкаОбъект.ПолучитьМакет("ТипыЭлементов");

	Построитель = Новый ПостроительЗапроса;
	Построитель.ИсточникДанных = Новый ОписаниеИсточникаДанных(ТабличныйДокумент.Область());  	
	
	ТаблицаЗначений = Построитель.Результат.Выгрузить();	
	
	Для Каждого СтрокаТаблицы Из ТаблицаЗначений Цикл
		Результат.Вставить(СокрЛП(СтрокаТаблицы.Название), СокрЛП(СтрокаТаблицы.НазваниеАнгл));
	КонецЦикла;
	
	Возврат Результат;
КонецФункции

#КонецОбласти

