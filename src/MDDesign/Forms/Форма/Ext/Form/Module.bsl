#Область ОписаниеПеременных

// Структура, хранящие в себе инициализированные модули
&НаКлиенте
Перем мМодули;

// Структура, хранящие в себе инициализированные модули
&НаКлиенте
Перем мВсеСвойства;

// Абстрактное синтаксическое дерево
&НаКлиенте
Перем мАСД;

// Текущая горизонтальная группа в редакторе
&НаКлиенте
Перем мТекущаяГруппа;

// Текущая строка таблицы группы
&НаКлиенте
Перем мТекущаяСтрокаГруппы;

// Текущая строка таблицы группы
&НаКлиенте
Перем мКонтекстОкнаHTML;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	СведенияОВнешнейОбработке = ОбработкаОбъект.СведенияОВнешнейОбработке();	
	
	Версия = СведенияОВнешнейОбработке.Версия;
	__ТекущаяВерсия = Версия;
	
	ЭтаФорма.Заголовок = СтрШаблон("Накидка. v%1", Версия);
	
	Элементы.__ДекорацияОднострочнаяГруппа.Заголовок = Элементы.__ДекорацияОднострочнаяГруппа.Заголовок + Формат(ТекущаяДатаСеанса(), "ДФ=dd.MM.yyyy"); 
	
	ЭтаФорма.__ИдентификаторОбработки = ОбработкаОбъект.Метаданные().ПолноеИмя();
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	ТекстHTML = ОбработкаОбъект.ПолучитьМакет("HTML").ПолучитьТекст();
	
	ТекстHTML = СтрЗаменить(ТекстHTML, "%ChevrotainJS%", ОбработкаОбъект.ПолучитьМакет("ChevrotainJS").ПолучитьТекст());
	ТекстHTML = СтрЗаменить(ТекстHTML, "%ParserJS%", ОбработкаОбъект.ПолучитьМакет("ParserJS").ПолучитьТекст());
	
	ПолеHTML = ТекстHTML;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Элементы.__ГруппаРедакторГрупп.Видимость = Ложь;

	мМодули = Новый Структура;
	
	ИнициализироватьМодули(
		"Парсер
		|ОпределениеРеквизитов
		|Синтаксис
		|Форматтер
		|УправлениеСвойствами
		|СозданиеРеквизитов
		|СлужебныеФункции");

	Если НЕ __НеПроверятьОбновления Тогда
		ПодключитьОбработчикОжидания("ПроверитьАктуальнуюВерсию", 0.1, Истина);
	КонецЕсли;
	
	__ОтображатьСправку = Истина;
	
	УстановитьВидимостьДоступностьЭлементов();    
	
	
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	Если __РежимРедактораГрупп Тогда   
		Попытка
			СинхронизироватьТаблицуГруппСГруппами();
			СтрокаФормат = Форматтер().Форматировать(мАСД);
			УстановитьТекстРедактора(СтрокаФормат, Ложь);
		Исключение
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = СтрШаблон("Ошибка при сохранении содержимого редактора: %1", ОписаниеОшибки());
			Сообщение.Сообщить();
		КонецПопытки;
	КонецЕсли; 
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура __ПостроитьФорму(Команда)   
	ДанныеГенератора = ПолучитьДанныеДляГенерацииФормы();
	Если ДанныеГенератора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Построить(ДанныеГенератора);
	
	ЗаголовокФормы = ДанныеГенератора.ЗаголовокФормы;
	Если ПустаяСтрока(ЗаголовокФормы) Тогда
		ЗаголовокФормы = "<Без заголовка>";
	КонецЕсли;
	
	__ОтображатьСправку = Ложь;
	
	УстановитьВидимостьДоступностьЭлементов();
	
	ЭтотОбъект.__ЗаголовокФормы = ЗаголовокФормы;
КонецПроцедуры   

&НаКлиенте
Процедура __ПостроитьФормуВОтдельномОкне(Команда)    
	ДанныеГенератора = ПолучитьДанныеДляГенерацииФормы();
	Если ДанныеГенератора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ФормаПросмотр = ОткрытьФорму(__ИдентификаторОбработки + ".Форма.ФормаПросмотр", , ЭтаФорма);
	ФормаПросмотр.Построить(ДанныеГенератора);
КонецПроцедуры

&НаКлиенте
Процедура __ГенераторКода(Команда)
	Если __РежимРедактораГрупп Тогда
		СинхронизироватьТаблицуГруппСГруппами();
	Иначе
		мАСД = Парсер().Разобрать(__Редактор);
	КонецЕсли;
	ОписанияРеквизитов = ОпределениеРеквизитов().Заполнить(мАСД); 
	
	УправлениеСвойствами().ЗаполнитьНеизвестныеСвойства(мАСД, мВсеСвойства);
	
	ДанныеГенератора = СозданиеРеквизитов().Получить(мАСД, ОписанияРеквизитов);

	ФормаПросмотр = ОткрытьФорму(__ИдентификаторОбработки + ".Форма.ФормаКода", , ЭтаФорма);
	
	ФормаПросмотр.Результат = ГенераторКода().Получить(ДанныеГенератора);
КонецПроцедуры

&НаКлиенте
Процедура __Форматировать(Команда)
	ФормаГруппы = Парсер().Разобрать(__Редактор);
	СтрокаФормат = Форматтер().Форматировать(ФормаГруппы);
	УстановитьТекстРедактора(СтрокаФормат);

	ЭтотОбъект.ТекущийЭлемент = Элементы.__Редактор;
КонецПроцедуры

&НаКлиенте
Процедура __ДобавитьКартинку(Команда)
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыборКартинкиЗавершение", ЭтотОбъект);
						 
	ОткрытьФорму(__ИдентификаторОбработки + ".Форма.ФормаВыбораКартинки", , ЭтаФорма, , , , ОписаниеОповещения);
КонецПроцедуры

&НаКлиенте
Процедура ВыборКартинкиЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	Если РезультатЗакрытия = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.__Редактор.ВыделенныйТекст = Лексема("Картинка") + РезультатЗакрытия;
	ОбновитьТекстВТекущейГруппе(ЭтотОбъект.__Редактор);	
КонецПроцедуры

&НаКлиенте
Процедура __РедакторГрупп(Команда)    
	Если __РежимРедактораГрупп Тогда
		СинхронизироватьТаблицуГруппСГруппами();   
		ТекРодитель = мТекущаяГруппа.Элементы[мТекущаяСтрокаГруппы.Порядок - 1];
	Иначе
		мАСД = Парсер().Разобрать(__Редактор);
		ТекРодитель = мАСД;
	КонецЕсли;
	
	ПараметрыДобавления = ПолучитьПарамерыДобавленияЭлемента(ТекРодитель, "ГоризонтальнаяГруппа");
	Если ПараметрыДобавления.Вместо <> Неопределено Тогда
		мТекущаяГруппа = ПараметрыДобавления.Вместо;
	Иначе        
		мТекущаяГруппа = Парсер().ПолучитьОписаниеГоризонтальнойГруппы(Неопределено);
		ДобавитьВставитьЭлемент(ПараметрыДобавления, мТекущаяГруппа);
		
		ВертикальнаяГруппа = Парсер().ПолучитьОписаниеВертикальнойГруппы(Неопределено);
		мТекущаяГруппа.Элементы.Добавить(ВертикальнаяГруппа);
	КонецЕсли;
	
	__ТаблицаГруппы.Очистить();
	Порядок = 1;
	Для Каждого ВертикальнаяГруппа Из мТекущаяГруппа.Элементы Цикл
		СтрокаТаблицы = __ТаблицаГруппы.Добавить();
		СтрокаТаблицы.Заголовок = ВертикальнаяГруппа.НаборСвойств.Заголовок;
		СтрокаТаблицы.Порядок = Порядок; 
		СтрокаТаблицы.Текст = Форматтер().Форматировать(ВертикальнаяГруппа);
		
		Порядок = Порядок + 1;
	КонецЦикла;
	
	Элементы.__ГруппаРедакторГрупп.Видимость = Истина;
	__РежимРедактораГрупп = Истина;      
	мТекущаяВертикальнаяГруппа = Неопределено;
КонецПроцедуры

&НаКлиенте
Процедура __ВРедактор(Команда)
	Элементы.__ГруппаРедакторГрупп.Видимость = Ложь; 
	__РежимРедактораГрупп = Ложь;
	
	СинхронизироватьТаблицуГруппСГруппами();
	
	СтрокаФормат = Форматтер().Форматировать(мАСД);
	УстановитьТекстРедактора(СтрокаФормат);
	
	ЭтотОбъект.ТекущийЭлемент = Элементы.__Редактор;
	
	мТекущаяГруппа = Неопределено;
КонецПроцедуры

&НаКлиенте
Процедура __РедакторТаблицы(Команда) 
	Если __РежимРедактораГрупп Тогда
		СинхронизироватьТаблицуГруппСГруппами();   
		ТекРодитель = мТекущаяГруппа.Элементы[мТекущаяСтрокаГруппы.Порядок - 1];
	Иначе
		мАСД = Парсер().Разобрать(__Редактор);
		ТекРодитель = мАСД;
	КонецЕсли;

	ПарамерыДобавления = ПолучитьПарамерыДобавленияЭлемента(ТекРодитель, "Таблица,Дерево");
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("ОписаниеТаблицы", ПарамерыДобавления.Вместо);
	
	ДополнительныеПараметры = Новый Структура("Родитель,Перед,Вместо,Форма", 
		ПарамерыДобавления.Родитель, 
		ПарамерыДобавления.Перед, 
		ПарамерыДобавления.Вместо,
		ПарамерыДобавления.Форма);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("РедактированиеТаблицыЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	
	ОткрытьФорму(__ИдентификаторОбработки + ".Форма.ФормаРедактированияТаблицы", ПараметрыОткрытия, ЭтотОбъект, , , , ОписаниеОповещения);
КонецПроцедуры

&НаКлиенте
Процедура РедактированиеТаблицыЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	Если РезультатЗакрытия = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	РодительЭлемента = ДополнительныеПараметры.Родитель;
	Если ЗначениеЗаполнено(ДополнительныеПараметры.Вместо) Тогда 
		Вместо = ДополнительныеПараметры.Вместо;
		ИндексЭлемента =  РодительЭлемента.Элементы.Найти(Вместо);
		РодительЭлемента.Элементы[ИндексЭлемента] = РезультатЗакрытия;	
	Иначе
		ДобавитьВставитьЭлемент(ДополнительныеПараметры, РезультатЗакрытия);
	КонецЕсли;

	СтрокаФормат = Форматтер().Форматировать(ДополнительныеПараметры.Форма); 
	УстановитьТекстРедактора(СтрокаФормат);
КонецПроцедуры
 
&НаКлиенте
Процедура __КомандаЗаглушка(Команда)
	// Ничего не делаем
КонецПроцедуры

&НаКлиенте
Процедура __ДобавитьПрямыеСкобки(Команда)
	ОбернутьВыделенныйТекст("[]", "");
КонецПроцедуры

&НаКлиенте
Процедура __ДобавитьФигурныеСкобки(Команда)
	ОбернутьВыделенныйТекст("{", "}");
КонецПроцедуры

&НаКлиенте
Процедура __ДобавитьУгловыеСкобки(Команда)
	ОбернутьВыделенныйТекст("<", ">");
КонецПроцедуры

&НаКлиенте
Процедура __Заполнить(Команда)
	ФормаГруппы = Парсер().Разобрать(__Редактор);
	ОписанияРеквизитов = ОпределениеРеквизитов().Заполнить(ФормаГруппы);
	
	ТипыРеквизитов = СтрРазделить("ПолеВвода,Флажок", ",");
	Для Каждого ТекущийТипРеквизита Из ТипыРеквизитов Цикл
		ЗаполнитьЗначениеСтрочногоЭлемента(ФормаГруппы, ОписанияРеквизитов, ТекущийТипРеквизита);
	КонецЦикла;
	
	ТипыРеквизитов = СтрРазделить("Таблица,Дерево", ",");
	Для Каждого ТекущийТипРеквизита Из ТипыРеквизитов Цикл
		ЗаполнитьЗначениеТабличногоЭлемента(ФормаГруппы, ОписанияРеквизитов, ТекущийТипРеквизита);
	КонецЦикла;
	
	СтрокаФормат = Форматтер().Форматировать(ФормаГруппы); 
	УстановитьТекстРедактора(СтрокаФормат);
КонецПроцедуры

&НаКлиенте
Процедура __ОткрытьДокументацию(Команда)
	ПерейтиПоНавигационнойСсылке("https://crimsongoldteam.github.io/md_design");
КонецПроцедуры

&НаКлиенте
Процедура __РедакторИИ(Команда)
	__ОтображатьЧатИИ = Не __ОтображатьЧатИИ;
	УстановитьВидимостьДоступностьЭлементов();
КонецПроцедуры

&НаКлиенте
Процедура __ОтправитьИИ(Команда)
	Если ПустаяСтрока(__ЗапросИИ) Тогда
		Возврат;
	КонецЕсли;
	
	__ОжиданиеОтветаИИ = Истина;
	
	Элементы.__ГруппаЗапросИИ.Доступность = НЕ __ОжиданиеОтветаИИ;

	ПодключитьОбработчикОжидания("ОтправитьИИЗавершение", 1, Истина);
КонецПроцедуры      

&НаКлиенте
Процедура __ОчиститьИИ(Команда)
	__ЧатИИ.Очистить();
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьИИЗавершение();
	__ОжиданиеОтветаИИ = Ложь;

	Элементы.__ГруппаЗапросИИ.Доступность = НЕ __ОжиданиеОтветаИИ;
	
	Контекст = Новый Массив;
	Для Каждого Строка Из __ЧатИИ Цикл
		Контекст.Добавить(Новый Структура("ЭтоВходящее,Сообщение", Строка.Входящее, Строка.Сообщение));
	КонецЦикла;
	
	Настройки = РаботаСИИ().ПолучитьНастройки();
	Настройки.ПровайдерИИ = __ПровайдерИИ;
	Настройки.МодельИИ = __МодельИИ;
	Настройки.ТокенИИ = __ТокенИИ;
	
	Ответ = РаботаСИИ().Отправить(__ЗапросИИ, Контекст, Настройки);
	
	Если Ответ = Неопределено Тогда
		ВызватьИсключение "Не удалось получить ответ!";
	КонецЕсли;

	СтрокаЧата = __ЧатИИ.Добавить();
	СтрокаЧата.Сообщение = __ЗапросИИ;
	__ЗапросИИ = "";   
	
	СтрокаЧата = __ЧатИИ.Добавить();
	СтрокаЧата.Сообщение = Ответ;
	СтрокаЧата.Входящее = Истина;
	
	Элементы.__ЧатИИ.ТекущаяСтрока = СтрокаЧата.ПолучитьИдентификатор();
КонецПроцедуры

&НаКлиенте
Процедура __ЗаменитьИИ(Команда)
	ТекущаяСтрока = Элементы.__ЧатИИ.ТекущиеДанные;
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Если НЕ ТекущаяСтрока.Входящее Тогда
		Возврат;
	КонецЕсли;
	
	ТекущаяСтрока.Сообщение = __Редактор;
КонецПроцедуры

&НаКлиенте
Процедура __ПоказатьСправку(Команда)
	__ОтображатьСправку = Истина;
	УстановитьВидимостьДоступностьЭлементов()
КонецПроцедуры

&НаКлиенте
Процедура __Настройки(Команда)                    
	Настройки = Новый Структура;
	
	ПоляНастроек = "__НеПроверятьОбновления,__ТокенИИ,__МодельИИ,__ПровайдерИИ";
	МассивНастроек = СтрРазделить(ПоляНастроек, ",");
	Для Каждого ИмяНастройки Из МассивНастроек Цикл
		Настройки.Вставить(ИмяНастройки, ЭтаФорма[ИмяНастройки]);
	КонецЦикла;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("НастройкиЗавершение", ЭтотОбъект);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Настройки", Настройки);
	ОткрытьФорму(__ИдентификаторОбработки + ".Форма.ФормаНастройка", ПараметрыФормы, ЭтотОбъект, , , , ОписаниеОповещения);
КонецПроцедуры

&НаКлиенте
Процедура НастройкиЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	Если РезультатЗакрытия = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого КлючЗначение Из РезультатЗакрытия Цикл
		ЭтаФорма[КлючЗначение.Ключ] = КлючЗначение.Значение;
	КонецЦикла; 
КонецПроцедуры
 

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура __РедакторПриИзменении(Элемент)                

	Если мТекущаяСтрокаГруппы  = Неопределено Тогда
		Возврат;
	КонецЕсли;  
	мТекущаяСтрокаГруппы.Текст = __Редактор;
	
КонецПроцедуры

&НаКлиенте
Процедура __РедакторИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка)
	//Попытка
	//	мАСД = Парсер().Разобрать(Текст);
	//	__ДеревоЭлементов.ПолучитьЭлементы().Очистить();     
	//	ЗаполнитьДеревоЭлементов(мАСД.Элементы, __ДеревоЭлементов.ПолучитьЭлементы());
	//Исключение
	//КонецПопытки;
КонецПроцедуры     


&НаКлиенте
Процедура НоваяВерсияНажатие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ПерейтиПоНавигационнойСсылке("https://github.com/crimsongoldteam/md_design/releases");
КонецПроцедуры

&НаКлиенте
Процедура __ДекорацияПримерНажатие(Элемент)
	МассивСтрок = Новый Массив;
	Если НЕ ПустаяСтрока(__Редактор) Тогда
		МассивСтрок.Добавить(__Редактор);	
	КонецЕсли;
	МассивСтрок.Добавить(Элемент.Заголовок);
	
	УстановитьТекстРедактора(СтрСоединить(МассивСтрок, Символы.ПС + Символы.ПС));
КонецПроцедуры

&НаКлиенте
Процедура ПолеHTMLДокументСформирован(Элемент)
	КонтекстДокументаHTML = Элемент.Документ;
	
	мКонтекстОкнаHTML = КонтекстДокументаHTML.parentWindow;
	Если мКонтекстОкнаHTML = Неопределено Тогда
		мКонтекстОкнаHTML = КонтекстДокументаHTML.defaultView;	
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыЧатИИ

&НаКлиенте
Процедура __ЧатИИВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ТекущаяСтрока = Элементы.__ЧатИИ.ТекущиеДанные;
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Если НЕ ТекущаяСтрока.Входящее Тогда
		Возврат;
	КонецЕсли;
	
	Текст = ТекущаяСтрока.Сообщение;
	УстановитьТекстРедактора(Текст);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТаблицаГруппы

&НаКлиенте
Процедура __ТаблицаГруппыПриАктивизацииСтроки(Элемент)
	мТекущаяСтрокаГруппы = Элементы.__ТаблицаГруппы.ТекущиеДанные;

	УстановитьТекстРедактора(мТекущаяСтрокаГруппы.Текст, Ложь);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции 

&НаКлиенте
Процедура ЗаполнитьДеревоЭлементов(ЭлементыАСД, ЭлементыДерева)
	Для Каждого ЭлементАСД Из ЭлементыАСД Цикл
		НовыйЭлементДерева = ЭлементыДерева.Добавить();
		НовыйЭлементДерева.Представление = ЭлементАСД.Тип;
		
		Если НЕ ЭлементАСД.Свойство("Элементы") Тогда
			Продолжить;
		КонецЕсли;
		ЗаполнитьДеревоЭлементов(ЭлементАСД.Элементы, НовыйЭлементДерева.ПолучитьЭлементы());
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Функция ПолучитьДанныеДляГенерацииФормы()
	Если __РежимРедактораГрупп Тогда
		СинхронизироватьТаблицуГруппСГруппами();
	Иначе
		мАСД = Парсер().Разобрать(__Редактор);
	КонецЕсли;   
	Если мАСД = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ОписанияРеквизитов = ОпределениеРеквизитов().Заполнить(мАСД); 
	
	УправлениеСвойствами().ЗаполнитьНеизвестныеСвойства(мАСД, мВсеСвойства);
	
	ДанныеГенератора = СозданиеРеквизитов().Получить(мАСД, ОписанияРеквизитов);
	Возврат ДанныеГенератора;
КонецФункции

&НаКлиенте
Процедура ЗаполнитьЗначениеТабличногоЭлемента(ФормаГруппы, ОписанияРеквизитов, ТипРеквизита)
	СоответствиеРеквизитов = ПолучитьСоответствиеРеквизитовИменам(ФормаГруппы, ОписанияРеквизитов, ТипРеквизита);
	Для Каждого КлючЗначение Из СоответствиеРеквизитов Цикл 
		Если НЕ ЕстьРеквизитФормы(КлючЗначение.Ключ, ТипРеквизита) Тогда
			Продолжить;
		КонецЕсли;
		ОписаниеЭлемента = КлючЗначение.Значение.ОписаниеЭлемента;
		ПрежнийУИД = КлючЗначение.Значение.ПрежнийУИД;
		СоответствиеКолонок = ПолучитьСоответствиеРеквизитовИменам(ОписаниеЭлемента, ОписанияРеквизитов, "Колонка", ПрежнийУИД);
		ТекущаяТаблица = ЭтотОбъект[КлючЗначение.Ключ];
		
		Если ТипРеквизита = "Таблица" Тогда
			ЗаполнитьОписанияПоСтрокамТаблицы(СоответствиеКолонок, ОписаниеЭлемента, ТекущаяТаблица);
		Иначе
			ЗаполнитьОписанияПоСтрокамДерева(СоответствиеКолонок, ОписаниеЭлемента, ТекущаяТаблица);
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьЗначениеСтрочногоЭлемента(ФормаГруппы, ОписанияРеквизитов, ТипРеквизита)
	СоответствиеРеквизитов = ПолучитьСоответствиеРеквизитовИменам(ФормаГруппы, ОписанияРеквизитов, ТипРеквизита);
	Для Каждого КлючЗначение Из СоответствиеРеквизитов Цикл 
		Если НЕ ЕстьРеквизитФормы(КлючЗначение.Ключ, ТипРеквизита) Тогда
			Продолжить;
		КонецЕсли;
		Значение = ЭтотОбъект[КлючЗначение.Ключ];
		ОписаниеЭлемента = КлючЗначение.Значение.ОписаниеЭлемента;
		ОписаниеЭлемента.Значение = Форматтер().ФорматироватьЗначениеПоля(Значение, ОписаниеЭлемента.ОписаниеТипов);
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьВставитьЭлемент(ПараметрыДобавления, НовыйЭлемент) 
	РодительЭлементы = ПараметрыДобавления.Родитель.Элементы;
	
	Если ЗначениеЗаполнено(ПараметрыДобавления.Перед) Тогда
		ИндексЭлемента =  РодительЭлементы.Найти(ПараметрыДобавления.Перед);
		РодительЭлементы.Вставить(ИндексЭлемента, НовыйЭлемент);
		Возврат;
	КонецЕсли;
	
	РодительЭлементы.Добавить(НовыйЭлемент);	
КонецПроцедуры

&НаКлиенте
Функция ПолучитьПарамерыДобавленияЭлемента(ФормаГруппы, СтрокаОжидаемыеТипы)     
	Результат = Новый Структура("Родитель,Вместо,Перед,Форма");

	НачалоСтроки = 0;
	НачалоКолонки = 0;
	КонецСтроки = 0;
	КонецКолонки = 0;

	ОжидаемыеТипы = СтрРазделить(СтрокаОжидаемыеТипы, ",");

	Элементы.__Редактор.ПолучитьГраницыВыделения(НачалоСтроки, НачалоКолонки, КонецСтроки, КонецКолонки);   
	ПозицияКурсора = ПолучитьБлижайшуюПозициюКурсора(НачалоСтроки, НачалоКолонки);

	Результат.Форма = ФормаГруппы;
	Если ПозицияКурсора.Пустая Тогда
		Результат.Родитель = ФормаГруппы;
		Возврат Результат;
	КонецЕсли;
	
	ИерархияЭлементов = Новый Массив;
	ПолучитьИерархиюЭлементовПоКоординатам(ИерархияЭлементов, ФормаГруппы, ПозицияКурсора.НомерСтроки, ПозицияКурсора.НомерКолонки);
	
	ТекущееОписаниеИндекс = ПолучитьТекущийЭлементИерархии(ИерархияЭлементов, ОжидаемыеТипы);
	
	Если ТекущееОписаниеИндекс <> Неопределено Тогда  
		Результат.Вместо = ИерархияЭлементов[ТекущееОписаниеИндекс];  
		Если ТекущееОписаниеИндекс = 0 Тогда
			Результат.Родитель = ФормаГруппы;
		Иначе
			Результат.Родитель = ИерархияЭлементов[ТекущееОписаниеИндекс - 1];
		КонецЕсли;
		Возврат Результат;
	КонецЕсли;	
	
	КоличествоЭлементов = ИерархияЭлементов.Количество();
	Если КоличествоЭлементов > 0 Тогда
		ТекущийПеред = ИерархияЭлементов[КоличествоЭлементов - 1];
		Результат.Перед = ТекущийПеред; 
		
		Если КоличествоЭлементов > 1 Тогда
			Результат.Родитель = ИерархияЭлементов[КоличествоЭлементов - 2]; 
		Иначе
			Результат.Родитель = ФормаГруппы;
		КонецЕсли;
	
		Возврат Результат;
	КонецЕсли;

	Результат.Родитель = ФормаГруппы;
	Возврат Результат;
КонецФункции

&НаКлиенте
Функция ПолучитьТекущийЭлементИерархии(ИерархияЭлементов, ОжидаемыеТипы)
	Если ИерархияЭлементов.Количество() = 0 Тогда
		ИерархияЭлементов.Добавить(мАСД);
	КонецЕсли;
	
	ТекущаяГруппа = Неопределено;
	
	Сч = ИерархияЭлементов.ВГраница();
	Пока Сч >= 0 Цикл
		ТекущееОписание = ИерархияЭлементов[Сч];
		Если ОжидаемыеТипы.Найти(ТекущееОписание.Тип) <> Неопределено Тогда
			Возврат Сч;
		КонецЕсли;
		
		Сч = Сч - 1;
	КонецЦикла;
	
	Возврат Неопределено;
КонецФункции

&НаКлиенте
Процедура УстановитьТекстРедактора(Значение, ОбновлятьГруппу = Истина)
	ЭтаФорма.__Редактор = Значение;  
	
	Если НЕ ОбновлятьГруппу Тогда
		Возврат;
	КонецЕсли;
	ОбновитьТекстВТекущейГруппе(Значение);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьТекстВТекущейГруппе(Значение)
	Если мТекущаяСтрокаГруппы = Неопределено Тогда 
		Возврат;
	КонецЕсли; 
	
	мТекущаяСтрокаГруппы.Текст = Значение;
КонецПроцедуры

&НаКлиенте
Процедура СинхронизироватьТаблицуГруппСГруппами()  
	Результат = Новый Массив;
	
	Порядок = 1;
	Для Каждого Строка Из __ТаблицаГруппы Цикл
		Если Строка.Порядок = 0 Тогда
			ВертикальнаяГруппа = Парсер().ПолучитьОписаниеВертикальнойГруппы(Неопределено);
		Иначе
			ВертикальнаяГруппа = мТекущаяГруппа.Элементы[Строка.Порядок - 1];
		КонецЕсли;
		
		ВертикальнаяГруппа.Элементы.Очистить();
		
		ВертикальнаяГруппаИзТекста = Парсер().Разобрать(Строка.Текст);
		Для Каждого ТекЭлемент Из ВертикальнаяГруппаИзТекста.Элементы Цикл
			ВертикальнаяГруппа.Элементы.Добавить(ТекЭлемент);
		КонецЦикла;
		
		ВертикальнаяГруппа.НаборСвойств.Заголовок = Строка.Заголовок;
		
		Результат.Добавить(ВертикальнаяГруппа);
		
		Строка.Порядок = Порядок;
		Порядок = Порядок + 1;
	КонецЦикла;
	
	мТекущаяГруппа.Элементы = Результат;
КонецПроцедуры

&НаКлиенте
Процедура ИнициализироватьМодули(ИменаМодулей)
	
	МассивМодулей = СтрРазделить(ИменаМодулей, Символы.ПС);
	Для Каждого Модуль Из МассивМодулей Цикл
		ИнициализироватьМодуль(Модуль);
	КонецЦикла;  
		
	мМодули.Синтаксис.Инициализировать();
	мВсеСвойства = мМодули.УправлениеСвойствами.ПолучитьВсеСвойства();
КонецПроцедуры

&НаКлиенте
Функция ИнициализироватьМодуль(ИмяМодуля)
	ТекИмя = СтрШаблон("%1.Форма.%2", __ИдентификаторОбработки, ИмяМодуля);
	
	// BSLLS:GetFormMethod-off 
	Результат = ПолучитьФорму(ТекИмя, , ЭтотОбъект);
	// BSLLS:GetFormMethod-on
	мМодули.Вставить(ИмяМодуля, Результат);
	
	Возврат Результат;
КонецФункции


&НаКлиенте
Процедура ЗаполнитьОписанияПоСтрокамДерева(СоответствиеКолонок, ОписаниеЭлемента, Родитель)

	Строки = Родитель.ПолучитьЭлементы();
	
	ИндексСтроки = 0;
	Для Каждого Строка Из Строки Цикл
		ОписаниеСтроки = ЗаполнитьОписанияПоСтроке(СоответствиеКолонок, ОписаниеЭлемента, Строка, ИндексСтроки);
		
		ЗаполнитьОписанияПоСтрокамДерева(СоответствиеКолонок, ОписаниеСтроки, Строка);
		
		ИндексСтроки = ИндексСтроки + 1;
	КонецЦикла;
	
	Пока ИндексСтроки <= ОписаниеЭлемента.Строки.ВГраница() Цикл
		ОписаниеЭлемента.Строки.Удалить(ОписаниеЭлемента.Строки.ВГраница());
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьОписанияПоСтрокамТаблицы(СоответствиеКолонок, ОписаниеЭлемента, Строки)
	
	ИндексСтроки = 0;
	Для Каждого Строка Из Строки Цикл
		ЗаполнитьОписанияПоСтроке(СоответствиеКолонок, ОписаниеЭлемента, Строка, ИндексСтроки);
		
		ИндексСтроки = ИндексСтроки + 1;
	КонецЦикла;
	
	Пока ИндексСтроки <= ОписаниеЭлемента.Строки.ВГраница() Цикл
		ОписаниеЭлемента.Строки.Удалить(ОписаниеЭлемента.Строки.ВГраница());
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Функция ЗаполнитьОписанияПоСтроке(СоответствиеКолонок, ОписаниеЭлемента, Строка, ИндексСтроки)
	Если ИндексСтроки > ОписаниеЭлемента.Строки.ВГраница() Тогда
		ОписаниеСтроки = Парсер().ПолучитьОписаниеСтрокиТаблицы(ОписаниеЭлемента);
	Иначе
		ОписаниеСтроки = ОписаниеЭлемента.Строки[ИндексСтроки];
	КонецЕсли;

	СоответствиеЯчейки = ПолучитьСоответствиеЯчеекПоКолонкам(ОписаниеСтроки.Ячейки);
	
	Для Каждого СоответствиеКолонок_КЗ Из СоответствиеКолонок Цикл
		ИмяРеквизита = СоответствиеКолонок_КЗ.Ключ;
		ОписаниеКолонки = СоответствиеКолонок_КЗ.Значение.ОписаниеЭлемента;
		
		Ячейка = СоответствиеЯчейки.Получить(ОписаниеКолонки.УИД);
		Если Ячейка = Неопределено Тогда
			Ячейка = Парсер().ПолучитьОписаниеЯчейкиТаблицы(ОписаниеСтроки);
			Ячейка.УИДКолонки = ОписаниеКолонки.УИД;
		КонецЕсли;          
		
		Ячейка.Значение = Форматтер().ФорматироватьЗначениеПоля(Строка[ИмяРеквизита], ОписаниеКолонки.ОписаниеТипов);
	КонецЦикла; 
	
	Возврат ОписаниеСтроки;
КонецФункции

&НаКлиенте
Функция ПолучитьСоответствиеЯчеекПоКолонкам(Ячейки)
	ЯчейкиПоКолонкам = Новый Соответствие;
	Для Каждого Ячейка Из Ячейки Цикл
		ЯчейкиПоКолонкам.Вставить(Ячейка.УИДКолонки, Ячейка);
	КонецЦикла;
	Возврат ЯчейкиПоКолонкам;
КонецФункции

&НаКлиенте
Функция ЕстьРеквизитФормы(ИмяРеквизита, ТипРеквизита)
	СтрокиСуществующийРеквизит = __ТаблицаСуществующихРеквизитов.НайтиСтроки(Новый Структура("ИмяРеквизита", ИмяРеквизита));
	Если СтрокиСуществующийРеквизит.Количество() = 0  Тогда
		Возврат Ложь; ;
	КонецЕсли;
	СтрокаСуществующийРеквизит = СтрокиСуществующийРеквизит[0];

	Возврат Истина;
КонецФункции

&НаКлиенте
Функция ПолучитьСоответствиеРеквизитовИменам(ФормаГруппы, Реквизиты, ТипРеквизита, УИДВладельца = Неопределено) Экспорт
	Результат = Новый Соответствие;
	Для Каждого Реквизит Из Реквизиты Цикл
		Если Реквизит.ТипРеквизита <> ТипРеквизита Тогда
			Продолжить;
		КонецЕсли;  
		
		Если УИДВладельца <> Неопределено И Реквизит.УИДВладельца <> УИДВладельца Тогда
			Продолжить;
		КонецЕсли;
		
		Если Реквизит.ПолноеИмяРеквизита = "" Тогда
			Продолжить;
		КонецЕсли;
		
		ТекОписаниеЭлемента = Парсер().ПолучитьЭлементПоУИД(ФормаГруппы, Реквизит.УИД);
		
		Если ТекОписаниеЭлемента = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Результат.Вставить(Реквизит.ПолноеИмяРеквизита, Новый Структура("ПрежнийУИД, ОписаниеЭлемента", Реквизит.УИД, ТекОписаниеЭлемента));
	КонецЦикла;
	
	Возврат Результат;
КонецФункции

&НаКлиенте
Функция ПолучитьБлижайшуюПозициюКурсора(Знач НомерСтроки, Знач НомерКолонки);
	Результат = Новый Структура("НомерСтроки,НомерКолонки,Пустая", НомерСтроки, НомерКолонки, Ложь);
	
	СтрокиРедактора = СтрРазделить(__Редактор, Символы.ПС);
	
	ИндексСтроки = Мин(СтрокиРедактора.ВГраница(), НомерСтроки - 1);
	Если ИндексСтроки < 0 Тогда
		Результат.НомерСтроки = 1;
		Результат.НомерКолонки = 1;
		Результат.Пустая = Истина;		
		Возврат Результат;
	КонецЕсли;
	
	Пока Истина Цикл   
		Если ИндексСтроки > СтрокиРедактора.ВГраница() Тогда
			Результат.НомерСтроки = 1;
			Результат.НомерКолонки = СтрокиРедактора.ВГраница() + 1; 
			Результат.Пустая = Истина;
			Возврат Результат;
		КонецЕсли;
		
		ТекущаяСтрока = СтрокиРедактора[ИндексСтроки];
		Если НЕ ПустаяСтрока(ТекущаяСтрока) Тогда
			Прервать;
		КонецЕсли;	
		
		ИндексСтроки = ИндексСтроки + 1;
		НомерКолонки = 1;  
		Результат.Пустая = Истина;		
		
		Продолжить;
	КонецЦикла;
	
	ДлинаСтроки = СтрДлина(СокрП(ТекущаяСтрока));
	Если НомерКолонки > ДлинаСтроки Тогда
		НомерКолонки = ДлинаСтроки;
	КонецЕсли;
	
	Результат.НомерСтроки = ИндексСтроки + 1;
	Результат.НомерКолонки = НомерКолонки;
	Возврат Результат;
КонецФункции

&НаКлиенте
Процедура ПолучитьИерархиюЭлементовПоКоординатам(ИерархияЭлементов, ОписаниеЭлемента, Верх, Лево)
	Интервалы = ОписаниеЭлемента.Координаты.Получить(Верх);
	
	Если Интервалы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Интервал Из Интервалы Цикл
		ВходитВИнтервал = Лево >= Интервал.Лево И Лево <= Интервал.Право;
		Если НЕ ВходитВИнтервал Тогда
			Продолжить;
		КонецЕсли;
		Если ОписаниеЭлемента.Тип = "Форма" Тогда
			Прервать;
		КонецЕсли;
		
		ИерархияЭлементов.Добавить(ОписаниеЭлемента); 
		Прервать;
	КонецЦикла;
	
	Если НЕ ВходитВИнтервал Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ОписаниеЭлемента.Свойство("Элементы") Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Подэлемент Из ОписаниеЭлемента.Элементы Цикл
		ПолучитьИерархиюЭлементовПоКоординатам(ИерархияЭлементов, Подэлемент, Верх, Лево);
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Функция Парсер() Экспорт
	Возврат ПолучитьМодуль("Парсер");
КонецФункции

&НаКлиенте
Функция ВыполнитьВнешнийПарсинг(Знач Текст) Экспорт
	Результат = мКонтекстОкнаHTML.parseInput(Текст); 
	
	Если СтрНачинаетсяС(Результат, "Ошибка:") Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = Результат;
		Сообщение.Сообщить();
		Возврат Неопределено;
	КонецЕсли;
	
	Если Результат = Неопределено Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Неизвестная ошибка парсинга!";
		Сообщение.Сообщить();
		Возврат Неопределено;
	КонецЕсли;
	
	РезультатСтруктура = СлужебныеФункции().JSONВСтруктуру(Результат);
	Возврат РезультатСтруктура;
КонецФункции

&НаКлиенте
Функция Синтаксис() Экспорт
	Возврат ПолучитьМодуль("Синтаксис");
КонецФункции

&НаКлиенте
Функция Форматтер() Экспорт
	Возврат ПолучитьМодуль("Форматтер");
КонецФункции     

&НаКлиенте
Функция СозданиеРеквизитов() Экспорт
	Возврат ПолучитьМодуль("СозданиеРеквизитов");
КонецФункции     

&НаКлиенте
Функция ГенераторКода() Экспорт
	Возврат ПолучитьМодуль("ГенераторКода");
КонецФункции     

&НаКлиенте
Функция РаботаСИИ() Экспорт
	Возврат ПолучитьМодуль("РаботаСИИ");
КонецФункции  

&НаКлиенте
Функция ПолучитьМодуль(ИмяМодуля)
	ФормаМодуля = Неопределено;
	
	Если НЕ мМодули.Свойство(ИмяМодуля, ФормаМодуля) Тогда
		ФормаМодуля = ИнициализироватьМодуль(ИмяМодуля); 
		мМодули.Вставить(ИмяМодуля, ФормаМодуля);
	КонецЕсли;
	Возврат ФормаМодуля;
КонецФункции

&НаКлиенте
Функция ОпределениеРеквизитов() Экспорт
	Возврат ПолучитьМодуль("ОпределениеРеквизитов");
КонецФункции     

&НаКлиенте
Функция УправлениеСвойствами() Экспорт
	Возврат ПолучитьМодуль("УправлениеСвойствами");
КонецФункции   

&НаКлиенте
Функция Лексема(ИмяПараметра) Экспорт
	Возврат Синтаксис().Лексема(ИмяПараметра);
КонецФункции

&НаКлиенте
Функция СлужебныеФункции() Экспорт
	Возврат ПолучитьМодуль("СлужебныеФункции");
КонецФункции

&НаКлиенте
Процедура СообщитьОбОшибкеПарсинга(Строка, Столбец, Сообщение, Отказ)
	Отказ = Истина;
	ТекстСообщения = СтрШаблон("%1 [%2, %3]", Сообщение, Строка, Столбец);
	
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = ТекстСообщения;
	Сообщение.Сообщить();  
	
	Элементы.__Редактор.УстановитьГраницыВыделения(Строка, Столбец, Строка, Столбец + 1);
КонецПроцедуры

&НаКлиенте
Функция ЗначениеВМассиве(Знач Значение)
	Результат = Новый Массив;
	Результат.Добавить(Значение);
	Возврат Результат;
КонецФункции

&НаКлиенте
Процедура Построить(ДанныеГенератора) Экспорт
	ПостроитьНаСервере(ДанныеГенератора);
КонецПроцедуры

&НаСервере
Процедура ПостроитьНаСервере(Знач ДанныеГенератора) Экспорт
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	ОбработкаОбъект.ПостроитьНаСервере(
		ЭтотОбъект, 
		Элементы.__ГруппаПросмотр, 
		ДанныеГенератора);
КонецПроцедуры

&НаКлиенте
Процедура ОбернутьВыделенныйТекст(СимволДо, СимволПосле)
	НачалоСтроки = 0;
	НачалоКолонки = 0;
	КонецСтроки = 0;
	КонецКолонки = 0;
	
	Элементы.__Редактор.ПолучитьГраницыВыделения(НачалоСтроки, НачалоКолонки, КонецСтроки, КонецКолонки);
	Элементы.__Редактор.ВыделенныйТекст = СимволДо + Элементы.__Редактор.ВыделенныйТекст + СимволПосле;
	Элементы.__Редактор.УстановитьГраницыВыделения(НачалоСтроки, НачалоКолонки + СтрДлина(СимволДо), КонецСтроки, КонецКолонки + СтрДлина(СимволДо));
	
	ОбновитьТекстВТекущейГруппе(ЭтотОбъект.__Редактор);
	
	ЭтотОбъект.ТекущийЭлемент = Элементы.__Редактор;
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьАктуальнуюВерсию()
	АктуальнаяВерсия = ПолучитьАктуальнуюВерсию(); 
	
	Если АктуальнаяВерсия = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ СравнитьВерсии(АктуальнаяВерсия, __ТекущаяВерсия) Тогда
		Возврат;
	КонецЕсли;
	
	//Элементы.__НоваяВерсия.Видимость = Истина;
	__НоваяВерсия = СтрШаблон("Доступна новая версия: %1", АктуальнаяВерсия);
КонецПроцедуры

&НаКлиенте
Функция СравнитьВерсии(АктуальнаяВерсия, ТекущаяВерсия)
	МассивАктуальнаяВерсия = СтрРазделить(АктуальнаяВерсия, ".");
	МассивТекущаяВерсия = СтрРазделить(ТекущаяВерсия, ".");
	Индекс = 0;
	Пока Индекс <= 2 Цикл
		НомерАктуальная = Число(МассивАктуальнаяВерсия[Индекс]);
		НомерТекущая = Число(МассивТекущаяВерсия[Индекс]);
		Если НомерТекущая > НомерАктуальная Тогда
			Возврат Ложь;
		КонецЕсли;
		Если НомерТекущая < НомерАктуальная Тогда
			Возврат Истина;
		КонецЕсли;
		
		Индекс = Индекс + 1;
	КонецЦикла;
	Возврат Ложь;
КонецФункции

&НаКлиенте
Функция ПолучитьАктуальнуюВерсию()
	URL = "repos/crimsongoldteam/md_design/releases";
	
	Попытка
	    Запрос = Новый HTTPЗапрос(URL);
		Соединение = Новый HTTPСоединение("api.github.com", , , , , 30, Новый ЗащищенноеСоединениеOpenSSL);
		Результат = Соединение.Получить(Запрос);     
		
		Если Результат.КодСостояния <> 200 Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		Тело = Результат.ПолучитьТелоКакСтроку();
		Результат = СлужебныеФункции().JSONВСтруктуру(Тело);
		Если Результат.Количество() = 0 Тогда
			Возврат Неопределено;
		КонецЕсли; 
		Версия = Сред(Результат[0]["tag_name"], 2);
	Исключение
		Возврат Неопределено;
	КонецПопытки;

	Возврат Версия;
КонецФункции

&НаКлиенте
Процедура УстановитьВидимостьДоступностьЭлементов()
	Элементы.__РедакторИИ.Пометка = __ОтображатьЧатИИ;
	Элементы.__ГруппаИИ.Видимость = __ОтображатьЧатИИ; 
	Элементы.__ГруппаСправка.Видимость = __ОтображатьСправку;
	Элементы.__ГруппаПросмотр.Видимость = НЕ __ОтображатьСправку;
	Элементы.__ПоказатьСправку.Видимость = НЕ __ОтображатьСправку;
	Элементы.__Заполнить.Видимость = НЕ __ОтображатьСправку;
	Элементы.__ЗаголовокФормы.Видимость = НЕ __ОтображатьСправку;  
	
	Элементы.__ПостроитьФорму.КнопкаПоУмолчанию = НЕ __ОтображатьЧатИИ;
	Элементы.__ОтправитьИИ.КнопкаПоУмолчанию = __ОтображатьЧатИИ;
КонецПроцедуры

#КонецОбласти  
