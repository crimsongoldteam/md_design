#Область ОписаниеПеременных

// Модуль настрооек синтаксиса
&НаКлиенте
Перем Синтаксис;

// Модуль парсинга текста
&НаКлиенте
Перем Парсер;

// Модуль определителя имен реквизитов и элементов
&НаКлиенте
Перем ОпределительРеквизитов;

// Модуль форматирования текста
&НаКлиенте
Перем Форматтер;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	СведенияОВнешнейОбработке = ОбработкаОбъект.СведенияОВнешнейОбработке();	
	
	Версия = СведенияОВнешнейОбработке.Версия;
	
	ЭтаФорма.Заголовок = СтрШаблон("MDDesign. v%1", Версия);
	
	ЭтаФорма.__ИдентификаторОбработки = ОбработкаОбъект.Метаданные().ПолноеИмя();	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	// BSLLS:GetFormMethod-off
	Парсер = ПолучитьФорму(__ИдентификаторОбработки + ".Форма.Парсер", , ЭтаФорма);
	ОпределительРеквизитов = ПолучитьФорму(__ИдентификаторОбработки + ".Форма.ОпределительРеквизитов", , ЭтаФорма);
	Синтаксис = ПолучитьФорму(__ИдентификаторОбработки + ".Форма.Синтаксис", , ЭтаФорма);
	Форматтер = ПолучитьФорму(__ИдентификаторОбработки + ".Форма.Форматтер", , ЭтаФорма);
	// BSLLS:GetFormMethod-on
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура __ПостроитьФорму(Команда)
	ФормаГруппы = Парсер().Разобрать(__Редактор);
	ОписанияРеквизитов = ОпределительРеквизитов().Заполнить(ФормаГруппы); 
	
	Построить(ФормаГруппы, ОписанияРеквизитов);
	
	ЗаголовокФормы = ФормаГруппы.НаборСвойств.Заголовок;
	Если ПустаяСтрока(ЗаголовокФормы) Тогда
		ЗаголовокФормы = "<Без заголовка>";
	КонецЕсли;
	
	ЭтотОбъект.__ЗаголовокФормы = ЗаголовокФормы;
КонецПроцедуры

&НаКлиенте
Процедура __ПостроитьФормуВОтдельномОкне(Команда)    
	ФормаГруппы = Парсер().Разобрать(__Редактор);
	ОписанияРеквизитов = ОпределительРеквизитов().Заполнить(ФормаГруппы); 
	
	ФормаПросмотр = ОткрытьФорму(__ИдентификаторОбработки + ".Форма.ФормаПросмотр", , ЭтаФорма);
	ФормаПросмотр.Построить(ФормаГруппы, ОписанияРеквизитов);
КонецПроцедуры

&НаКлиенте
Процедура __Форматировать(Команда)
	ФормаГруппы = Парсер().Разобрать(__Редактор);
	ЭтаФорма.__Редактор = Форматтер().Форматировать(ФормаГруппы);
	ЭтотОбъект.ТекущийЭлемент = Элементы.__Редактор;
КонецПроцедуры

&НаКлиенте
Процедура __ДобавитьКартинку(Команда)
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыборКартинкиЗавершение", ЭтотОбъект);
						 
	ОткрытьФорму(__ИдентификаторОбработки + ".Форма.ФормаВыбораКартинки", , ЭтаФорма, , , , ОписаниеОповещения);
КонецПроцедуры

&НаКлиенте
Процедура ВыборКартинкиЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	Если РезультатЗакрытия = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.__Редактор.ВыделенныйТекст = Лексема("Картинка") + РезультатЗакрытия;
КонецПроцедуры

&НаКлиенте
Процедура __РедакторТаблицы(Команда)                               
	НачалоСтроки = 0;
	НачалоКолонки = 0;
	КонецСтроки = 0;
	КонецКолонки = 0;
	
	Элементы.__Редактор.ПолучитьГраницыВыделения(НачалоСтроки, НачалоКолонки, КонецСтроки, КонецКолонки);   
	
	ПозицияКурсора = ПолучитьБлижайшуюПозициюКурсора(НачалоСтроки, НачалоКолонки);

	ФормаГруппы = Парсер().Разобрать(__Редактор);
	
	ИерархияЭлементов = Новый Массив;
	
	ПолучитьИерархиюЭлементовПоКоординатам(ИерархияЭлементов, ФормаГруппы, ПозицияКурсора.НомерСтроки, ПозицияКурсора.НомерКолонки);
	
	РодительЭлемента = Неопределено;
	Если ИерархияЭлементов.Количество() = 0 Тогда
		ИерархияЭлементов.Добавить(ФормаГруппы);
	КонецЕсли;
	
	ТекущееОписание = ИерархияЭлементов[ИерархияЭлементов.ВГраница()];
	
	ПередЭлементом = Неопределено;
	ВместоЭлемента = Неопределено;
	Если 
		НЕ ПозицияКурсора.Пустая
		И (ТекущееОписание.Тип = "Таблица"
		 ИЛИ ТекущееОписание.Тип = "Дерево") Тогда
		ВместоЭлемента = ТекущееОписание;
	Иначе
		ПередЭлементом = ТекущееОписание;
	КонецЕсли;

	
	Если ИерархияЭлементов.Количество() > 1 Тогда
		РодительЭлемента = ИерархияЭлементов[ИерархияЭлементов.ВГраница() - 1];
	Иначе
		РодительЭлемента = ФормаГруппы;
		ВместоЭлемента = Неопределено;
		ПередЭлементом = Неопределено;
	КонецЕсли;
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("ОписаниеТаблицы", ВместоЭлемента);
	
	ДополнительныеПараметры = Новый Структура("РодительЭлемента,ПередЭлементом,ВместоЭлемента,ФормаГруппы", РодительЭлемента, ПередЭлементом, ВместоЭлемента, ФормаГруппы);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("РедактированиеТаблицыЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	
	ОткрытьФорму(__ИдентификаторОбработки + ".Форма.ФормаРедактированияТаблицы", ПараметрыОткрытия, ЭтотОбъект, , , , ОписаниеОповещения);
КонецПроцедуры

&НаКлиенте
Процедура РедактированиеТаблицыЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	Если РезультатЗакрытия = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	РодительЭлемента = ДополнительныеПараметры.РодительЭлемента;
	Если ЗначениеЗаполнено(ДополнительныеПараметры.ВместоЭлемента) Тогда 
		ВместоЭлемента = ДополнительныеПараметры.ВместоЭлемента;
		ИндексЭлемента =  РодительЭлемента.Элементы.Найти(ВместоЭлемента);
		
		РодительЭлемента.Элементы[ИндексЭлемента] = РезультатЗакрытия;	
	Иначе
		Если ЗначениеЗаполнено(ДополнительныеПараметры.ПередЭлементом) Тогда
			ИндексЭлемента =  РодительЭлемента.Элементы.Найти(ДополнительныеПараметры.ПередЭлементом);
			РодительЭлемента.Элементы.Вставить(ИндексЭлемента, РезультатЗакрытия);	
		Иначе
			РодительЭлемента.Элементы.Добавить(РезультатЗакрытия);	
		КонецЕсли;
	КонецЕсли;

	ЭтаФорма.__Редактор = Форматтер().Форматировать(ДополнительныеПараметры.ФормаГруппы);
КонецПроцедуры
 
&НаКлиенте
Процедура __КомандаЗаглушка(Команда)
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "Заглушка";
	Сообщение.Сообщить();
КонецПроцедуры

&НаКлиенте
Процедура __ДобавитьПрямыеСкобки(Команда)
	ОбернутьВыделенныйТекст("[]", "");
КонецПроцедуры

&НаКлиенте
Процедура __ДобавитьФигурныеСкобки(Команда)
	ОбернутьВыделенныйТекст("{", "}");
КонецПроцедуры

&НаКлиенте
Процедура __ДобавитьУгловыеСкобки(Команда)
	ОбернутьВыделенныйТекст("<", ">");
КонецПроцедуры

&НаКлиенте
Процедура __Заполнить(Команда)
	ФормаГруппы = Парсер().Разобрать(__Редактор);
	ОписанияРеквизитов = ОпределительРеквизитов().Заполнить(ФормаГруппы);
	
	ТипыРеквизитов = СтрРазделить("ПолеВвода,Флажок", ",");
	Для Каждого ТекущийТипРеквизита Из ТипыРеквизитов Цикл
		ЗаполнитьЗначениеСтрочногоЭлемента(ФормаГруппы, ОписанияРеквизитов, ТекущийТипРеквизита);
	КонецЦикла;
	
	ТипыРеквизитов = СтрРазделить("Таблица,Дерево", ",");
	Для Каждого ТекущийТипРеквизита Из ТипыРеквизитов Цикл
		ЗаполнитьЗначениеТабличногоЭлемента(ФормаГруппы, ОписанияРеквизитов, ТекущийТипРеквизита);
	КонецЦикла;
	
	ЭтаФорма.__Редактор = Форматтер().Форматировать(ФормаГруппы);
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьЗначениеТабличногоЭлемента(ФормаГруппы, ОписанияРеквизитов, ТипРеквизита)
	СоответствиеРеквизитов = ПолучитьСоответствиеРеквизитовИменам(ФормаГруппы, ОписанияРеквизитов, ТипРеквизита);
	Для Каждого КлючЗначение Из СоответствиеРеквизитов Цикл 
		Если НЕ ЕстьРеквизитФормы(КлючЗначение.Ключ, ТипРеквизита) Тогда
			Продолжить;
		КонецЕсли;
		ОписаниеЭлемента = КлючЗначение.Значение.ОписаниеЭлемента;
		ПрежнийУИД = КлючЗначение.Значение.ПрежнийУИД;
		СоответствиеКолонок = ПолучитьСоответствиеРеквизитовИменам(ОписаниеЭлемента, ОписанияРеквизитов, "Колонка", ПрежнийУИД);
		ТекущаяТаблица = ЭтотОбъект[КлючЗначение.Ключ];
		
		Если ТипРеквизита = "Таблица" Тогда
			ЗаполнитьОписанияПоСтрокамТаблицы(СоответствиеКолонок, ОписаниеЭлемента, ТекущаяТаблица);
		Иначе
			ЗаполнитьОписанияПоСтрокамДерева(СоответствиеКолонок, ОписаниеЭлемента, ТекущаяТаблица);
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьЗначениеСтрочногоЭлемента(ФормаГруппы, ОписанияРеквизитов, ТипРеквизита)
	СоответствиеРеквизитов = ПолучитьСоответствиеРеквизитовИменам(ФормаГруппы, ОписанияРеквизитов, ТипРеквизита);
	Для Каждого КлючЗначение Из СоответствиеРеквизитов Цикл 
		Если НЕ ЕстьРеквизитФормы(КлючЗначение.Ключ, ТипРеквизита) Тогда
			Продолжить;
		КонецЕсли;
		Значение = ЭтотОбъект[КлючЗначение.Ключ];
		ОписаниеЭлемента = КлючЗначение.Значение.ОписаниеЭлемента;
		ОписаниеЭлемента.Значение = Значение;
	КонецЦикла;
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗаполнитьОписанияПоСтрокамДерева(СоответствиеКолонок, ОписаниеЭлемента, Родитель)

	Строки = Родитель.ПолучитьЭлементы();
	
	ИндексСтроки = 0;
	Для Каждого Строка Из Строки Цикл
		ОписаниеСтроки = ЗаполнитьОписанияПоСтроке(СоответствиеКолонок, ОписаниеЭлемента, Строка, ИндексСтроки);
		
		ЗаполнитьОписанияПоСтрокамДерева(СоответствиеКолонок, ОписаниеСтроки, Строка);
		
		ИндексСтроки = ИндексСтроки + 1;
	КонецЦикла;
	
	Пока ИндексСтроки <= ОписаниеЭлемента.Строки.ВГраница() Цикл
		ОписаниеЭлемента.Строки.Удалить(ОписаниеЭлемента.Строки.ВГраница());
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьОписанияПоСтрокамТаблицы(СоответствиеКолонок, ОписаниеЭлемента, Строки)
	
	ИндексСтроки = 0;
	Для Каждого Строка Из Строки Цикл
		ЗаполнитьОписанияПоСтроке(СоответствиеКолонок, ОписаниеЭлемента, Строка, ИндексСтроки);
		
		ИндексСтроки = ИндексСтроки + 1;
	КонецЦикла;
	
	Пока ИндексСтроки <= ОписаниеЭлемента.Строки.ВГраница() Цикл
		ОписаниеЭлемента.Строки.Удалить(ОписаниеЭлемента.Строки.ВГраница());
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Функция ЗаполнитьОписанияПоСтроке(СоответствиеКолонок, ОписаниеЭлемента, Строка, ИндексСтроки)
	Если ИндексСтроки > ОписаниеЭлемента.Строки.ВГраница() Тогда
		ОписаниеСтроки = Парсер().ПолучитьОписаниеСтрокиТаблицы(ОписаниеЭлемента);
	Иначе
		ОписаниеСтроки = ОписаниеЭлемента.Строки[ИндексСтроки];
	КонецЕсли;

	СоответствиеЯчейки = ПолучитьСоответствиеЯчеекПоКолонкам(ОписаниеСтроки.Ячейки);
	
	Для Каждого СоответствиеКолонок_КЗ Из СоответствиеКолонок Цикл
		ИмяРеквизита = СоответствиеКолонок_КЗ.Ключ;
		ОписаниеКолонки = СоответствиеКолонок_КЗ.Значение.ОписаниеЭлемента;
		
		Ячейка = СоответствиеЯчейки.Получить(ОписаниеКолонки.УИД);
		Если Ячейка = Неопределено Тогда
			Ячейка = Парсер().ПолучитьОписаниеЯчейкиТаблицы(ОписаниеСтроки);
			Ячейка.УИДКолонки = ОписаниеКолонки.УИД;
		КонецЕсли;
		Ячейка.Значение = Строка[ИмяРеквизита];
	КонецЦикла; 
	
	Возврат ОписаниеСтроки;
КонецФункции

&НаКлиенте
Функция ПолучитьСоответствиеЯчеекПоКолонкам(Ячейки)
	ЯчейкиПоКолонкам = Новый Соответствие;
	Для Каждого Ячейка Из Ячейки Цикл
		ЯчейкиПоКолонкам.Вставить(Ячейка.УИДКолонки, Ячейка);
	КонецЦикла;
	Возврат ЯчейкиПоКолонкам;
КонецФункции

&НаКлиенте
Функция ЕстьРеквизитФормы(ИмяРеквизита, ТипРеквизита)
	СтрокиСуществующийРеквизит = __ТаблицаСуществующихРеквизитов.НайтиСтроки(Новый Структура("ИмяРеквизита", ИмяРеквизита));
	Если СтрокиСуществующийРеквизит.Количество() = 0  Тогда
		Возврат Ложь;;
	КонецЕсли;
	СтрокаСуществующийРеквизит = СтрокиСуществующийРеквизит[0];
	//Если СтрокаСуществующийРеквизит.ТипРеквизита <> ТекущийТипРеквизита Тогда
	//	Продолжить;
	//КонецЕсли;
	
	Возврат Истина;
КонецФункции

&НаКлиенте
Функция ПолучитьСоответствиеРеквизитовИменам(ФормаГруппы, Реквизиты, ТипРеквизита, УИДВладельца = Неопределено) Экспорт
	Результат = Новый Соответствие;
	Для Каждого Реквизит Из Реквизиты Цикл
		Если Реквизит.ТипРеквизита <> ТипРеквизита Тогда
			Продолжить;
		КонецЕсли;  
		
		Если УИДВладельца <> Неопределено И Реквизит.УИДВладельца <> УИДВладельца Тогда
			Продолжить;
		КонецЕсли;
		
		Если Реквизит.ПолноеИмяРеквизита = "" Тогда
			Продолжить;
		КонецЕсли;
		
		ТекОписаниеЭлемента = Парсер().ПолучитьЭлементПоУИД(ФормаГруппы, Реквизит.УИД);
		
		Если ТекОписаниеЭлемента = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Результат.Вставить(Реквизит.ПолноеИмяРеквизита, Новый Структура("ПрежнийУИД, ОписаниеЭлемента", Реквизит.УИД, ТекОписаниеЭлемента));
	КонецЦикла;
	
	Возврат Результат;
КонецФункции

&НаКлиенте
Функция ПолучитьБлижайшуюПозициюКурсора(Знач НомерСтроки,Знач НомерКолонки);
	Результат = Новый Структура("НомерСтроки,НомерКолонки,Пустая", НомерСтроки, НомерКолонки, Ложь);
	
	СтрокиРедактора = СтрРазделить(__Редактор, Символы.ПС);
	
	ИндексСтроки = Мин(СтрокиРедактора.ВГраница(), НомерСтроки - 1);
	Если ИндексСтроки < 0 Тогда
		Результат.НомерСтроки = 1;
		Результат.НомерКолонки = 1;
		Результат.Пустая = Истина;		
		Возврат Результат;
	КонецЕсли;
	
	Пока Истина Цикл   
		Если ИндексСтроки > СтрокиРедактора.ВГраница() Тогда
			Результат.НомерСтроки = 1;
			Результат.НомерКолонки = СтрокиРедактора.ВГраница() + 1; 
			Результат.Пустая = Истина;
			Возврат Результат;
		КонецЕсли;
		
		ТекущаяСтрока = СтрокиРедактора[ИндексСтроки];
		Если НЕ ПустаяСтрока(ТекущаяСтрока) Тогда
			Прервать;
		КонецЕсли;	
		
		ИндексСтроки = ИндексСтроки + 1;
		НомерКолонки = 1;  
		Результат.Пустая = Истина;		
		
		Продолжить;
	КонецЦикла;
	
	ДлинаСтроки = СтрДлина(СокрП(ТекущаяСтрока));
	Если НомерКолонки > ДлинаСтроки Тогда
		НомерКолонки = ДлинаСтроки;
	КонецЕсли;
	
	Результат.НомерСтроки = ИндексСтроки + 1;
	Результат.НомерКолонки = НомерКолонки;
	Возврат Результат;
КонецФункции

&НаКлиенте
Процедура ПолучитьИерархиюЭлементовПоКоординатам(ИерархияЭлементов, ОписаниеЭлемента, Верх, Лево)
	Интервалы = ОписаниеЭлемента.Координаты.Получить(Верх);
	
	Если Интервалы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Интервал Из Интервалы Цикл
		ВходитВИнтервал = Лево >= Интервал.Лево И Лево <= Интервал.Право;
		Если НЕ ВходитВИнтервал Тогда
			Продолжить;
		КонецЕсли;
		
		ИерархияЭлементов.Добавить(ОписаниеЭлемента); 
		Прервать;
	КонецЦикла;
	
	Если НЕ ВходитВИнтервал Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ОписаниеЭлемента.Свойство("Элементы") Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Подэлемент Из ОписаниеЭлемента.Элементы Цикл
		ПолучитьИерархиюЭлементовПоКоординатам(ИерархияЭлементов, Подэлемент, Верх, Лево);
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Функция Парсер() Экспорт
	Возврат Парсер;
КонецФункции

&НаКлиенте
Функция Синтаксис() Экспорт
	Возврат Синтаксис;
КонецФункции

&НаКлиенте
Функция Форматтер() Экспорт
	Возврат Форматтер;
КонецФункции     

&НаКлиенте
Функция ОпределительРеквизитов() Экспорт
	Возврат ОпределительРеквизитов;
КонецФункции     

&НаКлиенте
Функция Лексема(ИмяПараметра) Экспорт
	Возврат Синтаксис().Лексема(ИмяПараметра);
КонецФункции

&НаКлиенте
Процедура СообщитьОбОшибкеПарсинга(Строка, Столбец, Сообщение, Отказ)
	Отказ = Истина;
	ТекстСообщения = СтрШаблон("%1 [%2, %3]", Сообщение, Строка, Столбец);
	
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = ТекстСообщения;
	Сообщение.Сообщить();  
	
	Элементы.__Редактор.УстановитьГраницыВыделения(Строка, Столбец, Строка, Столбец + 1);
КонецПроцедуры

&НаКлиенте
Функция ЗначениеВМассиве(Знач Значение)
	Результат = Новый Массив;
	Результат.Добавить(Значение);
	Возврат Результат;
КонецФункции

&НаКлиенте
Процедура Построить(ОписаниеГруппы, ОписанияРеквизитов) Экспорт
	ПостроитьНаСервере(ОписаниеГруппы, ОписанияРеквизитов);
КонецПроцедуры

&НаСервере
Процедура ПостроитьНаСервере(Знач ОписаниеГруппы, Знач ОписанияРеквизитов) Экспорт
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	ОбработкаОбъект.ПостроитьНаСервере(
		ЭтотОбъект, 
		Элементы.__ГруппаПросмотр, 
		ОписаниеГруппы, 
		ОписанияРеквизитов);
КонецПроцедуры

&НаКлиенте
Процедура ОбернутьВыделенныйТекст(СимволДо, СимволПосле)
	НачалоСтроки = 0;
	НачалоКолонки = 0;
	КонецСтроки = 0;
	КонецКолонки = 0;
	
	Элементы.__Редактор.ПолучитьГраницыВыделения(НачалоСтроки, НачалоКолонки, КонецСтроки, КонецКолонки);
	Элементы.__Редактор.ВыделенныйТекст = СимволДо + Элементы.__Редактор.ВыделенныйТекст + СимволПосле;
	Элементы.__Редактор.УстановитьГраницыВыделения(НачалоСтроки, НачалоКолонки + СтрДлина(СимволДо), КонецСтроки, КонецКолонки + СтрДлина(СимволДо));
	
	ЭтотОбъект.ТекущийЭлемент = Элементы.__Редактор;
КонецПроцедуры

#КонецОбласти  

