#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)       
	ВыгрузкаВРасширение = Параметры.Расширение <> Неопределено;
	Элементы.ЭтоРасширение.Видимость = ВыгрузкаВРасширение;
	
	ЭтоРасширение = ВыгрузкаВРасширение;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ЗаполнитьДеревоКонфигурации();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ЭтоРасширениеПриИзменении(Элемент)
	ЗаполнитьДеревоКонфигурации();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДеревоКонфигурации

&НаКлиенте
Процедура ДеревоКонфигурацииВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ТекущаяСтрока = Элементы.ДеревоКонфигурации.ТекущиеДанные;
	
	Если ТекущаяСтрока.ЭтоВид Тогда
		Возврат;
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("ИмяОбъекта", ТекущаяСтрока.Имя);
	Результат.Вставить("ТипОбъекта", ТекущаяСтрока.ПолучитьРодителя().Имя);
	Закрыть(Результат);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗаполнитьДеревоКонфигурации()
	ЭлементыДереваКорень = ДеревоКонфигурации.ПолучитьЭлементы();
	ЭлементыДереваКорень.Очистить();
		
	Для Каждого ОписаниеРаздела Из Параметры.ОписаниеРазделов Цикл
		Если НЕ ОписаниеРаздела.ДоступенКакОсновнойОбъект Тогда
			Продолжить;
		КонецЕсли;
		
		ЭлементДереваРаздел = ЭлементыДереваКорень.Добавить();
		ЭлементДереваРаздел.Имя = ОписаниеРаздела.Имя;
		ЭлементДереваРаздел.Картинка = ОписаниеРаздела.Картинка; 
		ЭлементДереваРаздел.ЭтоВид = Истина;
		
		Если ЭтоРасширение Тогда
			ОбъектыРаздела = Параметры.Расширение;
		Иначе
			ОбъектыРаздела = Параметры.Конфигурация;
		КонецЕсли;
		
		Если НЕ ОбъектыРаздела.Свойство(ОписаниеРаздела.Имя) Тогда
			Продолжить;
		КонецЕсли;         
		
		ПодчиненнныеЭлементы = ЭлементДереваРаздел.ПолучитьЭлементы();
		
		Для Каждого ОбъектРаздела Из ОбъектыРаздела[ОписаниеРаздела.Имя] Цикл
			ДобавляемыйЭлемент = ПодчиненнныеЭлементы.Добавить();
			ДобавляемыйЭлемент.Имя = ОбъектРаздела.Имя;
			ДобавляемыйЭлемент.Картинка = ОписаниеРаздела.Картинка;
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры

#КонецОбласти
