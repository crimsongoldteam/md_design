#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ЗаполнитьДеревоКонфигурации();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДеревоКонфигурации

&НаКлиенте
Процедура ДеревоКонфигурацииВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ТекущаяСтрока = Элементы.ДеревоКонфигурации.ТекущиеДанные;
	
	Если ТекущаяСтрока.ЭтоВид Тогда
		Возврат;
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("ИмяОбъекта", ТекущаяСтрока.ИмяОбъекта);
	Результат.Вставить("ТипОбъекта", ТекущаяСтрока.ПолучитьРодителя().ИмяОбъекта);
	Закрыть(Результат);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗаполнитьДеревоКонфигурации()
	ИмяФайлаConfiguration = Параметры.КаталогXML + "\Configuration.xml";
	
	ЭлементыДереваКорень = ДеревоКонфигурации.ПолучитьЭлементы();
	
	ЭлементыДереваКорень.Очистить();
	
	ЭлементыДереваСправочник = ЭлементыДереваКорень.Добавить();
	ЭлементыДереваСправочник.ИмяОбъекта = "Справочник";
	ЭлементыДереваСправочник.Картинка = 3;
	ЭлементыДереваСправочник.ЭтоВид = Истина;
	ЭлементыСправочники = ЭлементыДереваСправочник.ПолучитьЭлементы();

	ЭлементыДереваДокумент = ЭлементыДереваКорень.Добавить();
	ЭлементыДереваДокумент.ИмяОбъекта = "Документ"; 
	ЭлементыДереваДокумент.Картинка = 7;
	ЭлементыДереваДокумент.ЭтоВид = Истина;
	ЭлементыДокументы = ЭлементыДереваДокумент.ПолучитьЭлементы();
	
	СоответетствиеГруппУзлам = Новый Соответствие;
	СоответетствиеГруппУзлам.Вставить("Catalog", ЭлементыСправочники);
	СоответетствиеГруппУзлам.Вставить("Document", ЭлементыДокументы);

	СоответетствиеКартинокУзлам = Новый Соответствие;
	СоответетствиеКартинокУзлам.Вставить("Catalog", 3);
	СоответетствиеКартинокУзлам.Вставить("Document", 7);
	
	ЧтениеXML = Новый ЧтениеXML; 
	ЧтениеXML.ОткрытьФайл(ИмяФайлаConfiguration); 
	
	ПостроительDOM = Новый ПостроительDOM;
	ДокументDOM = ПостроительDOM.Прочитать(ЧтениеXML);
	ЧтениеXML.Закрыть();
	
	
	УзелMetaDataObject = ДокументDOM.ПервыйДочерний;
	УзелConfiguration = УзелMetaDataObject.ПервыйДочерний;
	МассивУзлыChildObjects = УзелConfiguration.ПолучитьЭлементыПоИмени("ChildObjects"); 
	УзлыChildObjects = МассивУзлыChildObjects[0].ДочерниеУзлы;
	
	Для Каждого УзелChildObjects Из УзлыChildObjects Цикл
		ПодчиненнныеЭлементы = СоответетствиеГруппУзлам.Получить(УзелChildObjects.ИмяУзла);
		Если ПодчиненнныеЭлементы = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ДобавляемыйЭлемент = ПодчиненнныеЭлементы.Добавить();
		ДобавляемыйЭлемент.ИмяОбъекта = УзелChildObjects.ПоследнийДочерний.ЗначениеУзла;
		ДобавляемыйЭлемент.Картинка = СоответетствиеКартинокУзлам.Получить(УзелChildObjects.ИмяУзла);
	КонецЦикла;
КонецПроцедуры

#КонецОбласти
