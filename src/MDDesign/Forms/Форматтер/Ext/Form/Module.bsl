// MIT License

// Copyright (c) 2025 Zherebtsov Nikita <nikita@crimsongold.ru>

// Permission is hereby granted, free of charge, to any person obtaining a copy
// of this software and associated documentation files (the "Software"), to deal
// in the Software without restriction, including without limitation the rights
// to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
// copies of the Software, and to permit persons to whom the Software is
// furnished to do so, subject to the following conditions:

// The above copyright notice and this permission notice shall be included in all
// copies or substantial portions of the Software.

// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
// IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
// FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
// AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
// LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
// OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
// SOFTWARE.

// https://github.com/crimsongoldteam/md_design

#Область ОбработчикиСобытийФормы

&НаКлиенте
Функция Форматировать(ФормаГруппы) Экспорт
	ПараметрыВыполнения = НовыеПараметрыФорматирования(Истина, 0);
	ВыполнитьФорматирование(ПараметрыВыполнения, ФормаГруппы);
	
	ФормаГруппы.Формат.Результат.Очистить();
	
	ПараметрыВыполнения = НовыеПараметрыФорматирования(Ложь, 0);
	ВыполнитьФорматирование(ПараметрыВыполнения, ФормаГруппы);
	
	Результат = Новый Массив;  
	
	ДобавитьЗаголовокФормы(Результат, ФормаГруппы);
	
	Для Каждого Строка Из ФормаГруппы.Формат.Результат Цикл
		Результат.Добавить(СокрП(Строка)); 	
	КонецЦикла;
	
	Возврат СокрП(СтрСоединить(Результат, Символы.ПС));
КонецФункции

&НаКлиенте
Процедура ДобавитьЗаголовокФормы(Результат, ФормаГруппы)
	Если ФормаГруппы.Тип <> "Форма" Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ФормаГруппы.НаборСвойств.Свойство("Заголовок") Тогда
		Возврат;
	КонецЕсли;

	ЗаголовокФормы = ФормаГруппы.НаборСвойств.Заголовок;
	
	Если ПустаяСтрока(ЗаголовокФормы) Тогда
		Возврат;
	КонецЕсли;
	
	РазделительЗаголовок = Лексема("ФормаЗаголовок");
	ЗаголовокФормы = РазделительЗаголовок + " " + ЗаголовокФормы + " " + РазделительЗаголовок;
	Результат.Добавить(ЗаголовокФормы);
	Результат.Добавить("");
КонецПроцедуры

&НаКлиенте
Функция ФорматироватьЗначениеПоля(Значение, ОписаниеТипов) Экспорт 
	ТипЗначения = ТипЗнч(Значение); 
	Если ТипЗначения = Тип("Число") Тогда
		Возврат ФорматироватьЗначениеЧисло(Значение, ОписаниеТипов);
	КонецЕсли;

	Если ТипЗначения = Тип("Дата") Тогда
		Возврат ФорматироватьЗначениеДата(Значение, ОписаниеТипов);
	КонецЕсли;

	Возврат Значение; 
КонецФункции

&НаКлиенте
Функция ФорматироватьОписаниеТипов(ОписаниеТипов) Экспорт
	Результат = Новый Массив;
	Если ОписаниеТипов.Авто Тогда 
		Возврат "";
	КонецЕсли;

	ФорматированиеЧисла = СлужебныеФункции().ПолучитьПредставлениеТипаЧисла(ОписаниеТипов, "Число", Истина);
	Если ФорматированиеЧисла <> Неопределено Тогда
		Результат.Добавить(ФорматированиеЧисла);
	КонецЕсли;	

	ФорматированиеСтроки = СлужебныеФункции().ПолучитьПредставлениеТипаСтроки(ОписаниеТипов, "Строка", Истина);
	Если ФорматированиеСтроки <> Неопределено Тогда
		Результат.Добавить(ФорматированиеСтроки);
	КонецЕсли;	

	ФорматированиеДаты = СлужебныеФункции().ПолучитьПредставлениеТипаДаты(ОписаниеТипов, "Дата", Истина);
	Если ФорматированиеДаты <> Неопределено Тогда
		Результат.Добавить(ФорматированиеДаты);
	КонецЕсли;	
	
	Для Каждого ТекТип Из ОписаниеТипов.Типы Цикл
		Если ТекТип = "Число" ИЛИ ТекТип = "Дата" ИЛИ ТекТип = "Строка" Тогда
			Продолжить;
		КонецЕсли; 
		Результат.Добавить(ТекТип);
	КонецЦикла;
	
	Возврат СтрСоединить(Результат, ", ");
КонецФункции


#КонецОбласти    

#Область СлужебныеПроцедурыИФункции

#Область Модули

&НаКлиенте
Функция Лексема(ИмяПараметра)
	Возврат ВладелецФормы.Лексема(ИмяПараметра);
КонецФункции

&НаКлиенте
Функция СлужебныеФункции()
	Возврат ВладелецФормы.СлужебныеФункции();
КонецФункции

#КонецОбласти

&НаКлиенте
Функция ФорматироватьЗначениеЧисло(Значение, ОписаниеТипов)
	МассивПараметров = Новый Массив;
	МассивПараметров.Добавить("ЧРД=."); 
	МассивПараметров.Добавить("ЧГ=0");
	Если ОписаниеТипов.ДлинаЧисла <> Неопределено И ОписаниеТипов.ДлинаЧисла <> 0 Тогда
		МассивПараметров.Добавить("ЧЦ=" + Формат(ОписаниеТипов.ДлинаЧисла,"ЧГ=0"));
	КонецЕсли;
	Если ОписаниеТипов.ТочностьЧисла <> Неопределено И ОписаниеТипов.ТочностьЧисла <> 0 Тогда
		МассивПараметров.Добавить("ЧДЦ=" + Формат(ОписаниеТипов.ТочностьЧисла,"ЧГ=0"));
		МассивПараметров.Добавить("ЧН=0." + СтрПовторитьСимвол("0", ОписаниеТипов.ТочностьЧисла));
	Иначе	
		МассивПараметров.Добавить("ЧН=0");
	КонецЕсли;	
	
	ФорматнаяСтрока = СтрСоединить(МассивПараметров, ";");
	Возврат Формат(Значение, ФорматнаяСтрока);;
КонецФункции

&НаКлиенте
Функция ФорматироватьЗначениеДата(Значение, ОписаниеТипов) 
	ФорматнаяСтрока = "ДФ='dd.MM.yyyy HH:mm:ss'; ДП='01.01.0001 00:00:00'";

	Если ОписаниеТипов.ЧастиДаты = "Дата" Тогда
		ФорматнаяСтрока = "ДФ='dd.MM.yyyy'; ДП='01.01.0001'";
	ИначеЕсли ОписаниеТипов.ЧастиДаты = "Время" Тогда 
		ФорматнаяСтрока = "ДФ='HH:mm:ss'; ДП='00:00:00'";
	Иначе
		ФорматнаяСтрока = "ДФ='dd.MM.yyyy HH:mm:ss'; ДП='01.01.0001 00:00:00'";
	КонецЕсли;
	
	Возврат Формат(Значение, ФорматнаяСтрока);;
КонецФункции

&НаКлиенте
Функция НовыеПараметрыФорматирования(РасчетДлины, Длина)
	Результат = Новый Структура();
	Результат.Вставить("РасчетДлины", РасчетДлины);
	Результат.Вставить("Длина", ?(РасчетДлины, 0, Длина));
	
	Возврат Результат;
КонецФункции

&НаКлиенте
Процедура ВыполнитьФорматирование(ПараметрыВыполнения, ЭлементРодитель)    
	
	Подчиненные = ЭлементРодитель.Элементы;
	
	Первый = Истина;
	РазрывСтрок = Ложь;
	Для Каждого ЭлементДанных Из Подчиненные Цикл 
		
		Выполнена = Ложь;
		ФорматироватьВертикальнуюГруппу(ПараметрыВыполнения, ЭлементДанных, Выполнена);
		Если Выполнена Тогда
			Продолжить;
		КонецЕсли;
		
		ФорматироватьСтрочныйЭлемент(ПараметрыВыполнения, ЭлементРодитель, ЭлементДанных);
		
		Если ЭлементДанных.Формат.РазрывСтрок Тогда
			РазрывСтрок = Истина;
		КонецЕсли;
		
		Если НЕ Первый И РазрывСтрок Тогда
			ПустаяСтрока = СтрПовторитьСимвол(" ", ЭлементРодитель.Формат.Длина);
			ФорматированиеДобавитьРезультат(ЭлементРодитель, ПустаяСтрока);
		КонецЕсли;

		Если НЕ ЭлементДанных.Формат.РазрывСтрок Тогда
			РазрывСтрок = Ложь;
		КонецЕсли;
		
		ФорматированиеДобавитьРезультат(ЭлементРодитель, ЭлементДанных.Формат.Результат);
		
		Первый = Ложь;
	КонецЦикла;	
КонецПроцедуры

&НаКлиенте
Процедура ФорматироватьСтрочныйЭлемент(ПараметрыВыполнения, ЭлементРодитель, ЭлементДанных)
	ЭлементДанных.Формат.Результат.Очистить();
	Выполнена = Ложь;
	
	ФорматироватьГоризонтальнуюГруппу(ПараметрыВыполнения, ЭлементДанных, Выполнена);
	Если Выполнена Тогда
		Возврат;
	КонецЕсли;

	ФорматироватьОднострочнуюГруппу(ПараметрыВыполнения, ЭлементДанных, Выполнена);
	Если Выполнена Тогда
		Возврат;
	КонецЕсли;
	
	ФорматироватьТаблицу(ПараметрыВыполнения, ЭлементДанных, Выполнена);
	Если Выполнена Тогда
		Возврат;
	КонецЕсли;

	ФорматироватьПолеВвода(ПараметрыВыполнения, ЭлементДанных, Выполнена);
	Если Выполнена Тогда
		Возврат;
	КонецЕсли;

	ФорматироватьФлажок(ПараметрыВыполнения, ЭлементДанных, Выполнена);
	Если Выполнена Тогда
		Возврат;
	КонецЕсли;

	ФорматироватьНадпись(ПараметрыВыполнения, ЭлементДанных, Выполнена);
	Если Выполнена Тогда
		Возврат;
	КонецЕсли;
	
	ФорматироватьСтраницы(ПараметрыВыполнения, ЭлементДанных, Выполнена);
	Если Выполнена Тогда
		Возврат;
	КонецЕсли;

	ФорматироватьКоманднуюПанель(ПараметрыВыполнения, ЭлементДанных, Выполнена);
	Если Выполнена Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

#Область ФорматироватьГруппу

&НаКлиенте
Процедура ФорматироватьВертикальнуюГруппу(ПараметрыВыполнения, ЭлементДанных, Выполнена)
	Если НЕ ЭлементДанных.Тип = "ВертикальнаяГруппа" Тогда
		Возврат;
	КонецЕсли;
	Выполнена = Истина;
	
	ВыполнитьФорматирование(ПараметрыВыполнения, ЭлементДанных);
КонецПроцедуры

&НаКлиенте
Функция ПолучитьЗаголовокВертикальнойГруппы(ВертикальнаяГруппа, Первая)
	ИсключаемыеСвойства = Новый Массив;
	ИсключаемыеСвойства.Добавить("Заголовок");	
	ИсключаемыеСвойства.Добавить("Поведение");	
	
	ОтображениеПоведение = ПолучитьОтображениеПоведениеГруппы(ВертикальнаяГруппа);
	
	Если НЕ ОтображениеПоведение.Отображение Тогда
		ИсключаемыеСвойства.Добавить("Отображение");	
	КонецЕсли;

	Уровень = ОтображениеПоведение.Уровень;
	// Для первой делаем все #, для остальных один символ будет в соединении
	Если НЕ Первая Тогда
		Уровень = Уровень - 1;
	КонецЕсли;
	
	СтрокаЗаголовок = СтрПовторитьСимвол(Лексема("РазделительГруппаДобавить"), Уровень);
	Если ВертикальнаяГруппа.НаборСвойств.Свойство("Заголовок") 
		И НЕ ПустаяСтрока(ВертикальнаяГруппа.НаборСвойств.Заголовок) Тогда
		СтрокаЗаголовок = СтрокаЗаголовок + СокрЛП(ВертикальнаяГруппа.НаборСвойств.Заголовок);			
	КонецЕсли;
	СтрокаЗаголовок = СтрокаЗаголовок + ФорматироватьНаборСвойств(ВертикальнаяГруппа.НаборСвойств, ИсключаемыеСвойства);
	
	Возврат ВыровнитьСтроку(СтрокаЗаголовок, "Лево", ВертикальнаяГруппа.Формат.Длина);
КонецФункции

&НаКлиенте
Функция ПолучитьНаборСтрокВертикальнаяГруппа(ПараметрыВыполнения, ВертикальнаяГруппа, Первая)
	Результат = Новый Массив;
	
	ВертикальнаяГруппа.Формат.Результат.Очистить();

	ПараметрыВыполненияРасчет = НовыеПараметрыФорматирования(
		ПараметрыВыполнения.РасчетДлины, 
		ВертикальнаяГруппа.Формат.Длина);
	ВыполнитьФорматирование(ПараметрыВыполненияРасчет, ВертикальнаяГруппа);
	
	СтрокаЗаголовок = ПолучитьЗаголовокВертикальнойГруппы(ВертикальнаяГруппа, Первая);
	Результат.Добавить(СтрокаЗаголовок);
	
	Если ПараметрыВыполнения.РасчетДлины Тогда
		ВертикальнаяГруппа.Формат.Длина = СтрДлина(СтрокаЗаголовок);
	КонецЕсли;
	
	Для Каждого Строка Из ВертикальнаяГруппа.Формат.Результат Цикл 
		ТекСтрока = Строка;
		Если Первая Тогда
			ТекСтрока = "  " + СокрЛП(ТекСтрока);
		КонецЕсли;
		
		ТекСтрока = ВыровнитьСтроку(ТекСтрока, "Лево", ВертикальнаяГруппа.Формат.Длина);
		
		Если ПараметрыВыполнения.РасчетДлины Тогда
			ВертикальнаяГруппа.Формат.Длина = Макс(ВертикальнаяГруппа.Формат.Длина, СтрДлина(ТекСтрока));
		КонецЕсли;
		
		Результат.Добавить(ТекСтрока);
	КонецЦикла; 
	
	
	Возврат Результат;
КонецФункции 
	
&НаКлиенте
Процедура ФорматироватьГоризонтальнуюГруппу(ПараметрыВыполнения, ЭлементДанных, Выполнена)
	Если НЕ ЭлементДанных.Тип = "ГоризонтальнаяГруппа" Тогда
		Возврат;
	КонецЕсли;
	Выполнена = Истина;
	РазделительПробел = " ";
	
	МаксКоличествоСтрок = 0;
	НаборСтрок = Новый Массив; 
	ПерваяГруппа = Истина;
	Для Каждого ВертикальнаяГруппа Из ЭлементДанных.Элементы Цикл
		ЭлементНабора = ПолучитьНаборСтрокВертикальнаяГруппа(ПараметрыВыполнения, ВертикальнаяГруппа, ПерваяГруппа);
		НаборСтрок.Добавить(ЭлементНабора);
		МаксКоличествоСтрок = Макс(МаксКоличествоСтрок, ЭлементНабора.Количество());  
		
		ПерваяГруппа = Ложь;
	КонецЦикла;
	
	Индекс = 0;
	Для Каждого ВертикальнаяГруппа Из ЭлементДанных.Элементы Цикл
		ТекКоличество = НаборСтрок[Индекс].Количество();
		ПустаяСтрока = СтрПовторитьСимвол(" ",  ВертикальнаяГруппа.Формат.Длина); 
		Для Сч = 1 По МаксКоличествоСтрок - ТекКоличество Цикл
			НаборСтрок[Индекс].Добавить(ПустаяСтрока);
		КонецЦикла;
		
		Индекс = Индекс + 1;
	КонецЦикла;
	
	
	Результат = СоединитьСтрокиГоризонтально(
		НаборСтрок, 
		РазделительПробел + Лексема("РазделительГруппа"),
		РазделительПробел + Лексема("РазделительГруппаДобавить"));
	ФорматированиеДобавитьРезультат(ЭлементДанных, Результат);
	
	ФорматированиеДобавитьРезультат(ЭлементДанных, "");
КонецПроцедуры

&НаКлиенте
Функция ПолучитьОтображениеПоведениеГруппы(ВертикальнаяГруппа)
	Результат = Новый Структура("Уровень,Отображение", 1, Ложь);
	
	Отображение = ПолучитьСвойствоИлиНеопределено(ВертикальнаяГруппа, "Отображение");
	Поведение = ПолучитьСвойствоИлиНеопределено(ВертикальнаяГруппа, "Поведение");
	
	СтрПоведениеУровней = Новый Структура;
	СтрПоведениеУровней.Вставить("Свертываемая", 5);
	СтрПоведениеУровней.Вставить("Всплывающая", 6); 
	
	Если Поведение <> Неопределено И СтрПоведениеУровней.Свойство(Поведение) Тогда
		Результат.Уровень = СтрПоведениеУровней[Поведение]; 
		
		Если НРег(Отображение) <> НРег("ОбычноеВыделение") Тогда
			Результат.Отображение = Истина;
		КонецЕсли; 
		
		Возврат Результат;
	КонецЕсли;  
	
	СтрОтображенияУровней = Новый Структура;
	СтрОтображенияУровней.Вставить("Нет", 1);
	СтрОтображенияУровней.Вставить("СлабоеВыделение", 2); 
	СтрОтображенияУровней.Вставить("ОбычноеВыделение", 3);
	СтрОтображенияУровней.Вставить("СильноеВыделение", 4);
	
	Если Отображение = Неопределено Тогда
		Возврат Результат;
	КонецЕсли; 
	
	Если НЕ СтрОтображенияУровней.Свойство(Отображение) Тогда
		Возврат Результат;
	КонецЕсли; 
	
	Результат.Уровень = СтрОтображенияУровней[Отображение]; 
	
	Возврат Результат;
КонецФункции

&НаКлиенте
Функция ПолучитьСвойствоИлиНеопределено(Элемент, ИмяСвойсва)
	Если НЕ Элемент.НаборСвойств.Свойство(ИмяСвойсва) Тогда
		Возврат Неопределено;
	КонецЕсли;

	Возврат Элемент.НаборСвойств[ИмяСвойсва];
КонецФункции

&НаКлиенте
Процедура ФорматироватьОднострочнуюГруппу(ПараметрыВыполнения, ЭлементДанных, Выполнена)
	Если НЕ ЭлементДанных.Тип = "ОднострочнаяГруппа" Тогда
		Возврат;
	КонецЕсли;
	Выполнена = Истина;

	НаборСтрок = Новый Массив;
	Для Каждого ЭлементГруппы Из ЭлементДанных.Элементы Цикл
		ЭлементГруппы.Формат.Результат.Очистить();
		
		ПараметрыВыполненияРасчет = НовыеПараметрыФорматирования(ПараметрыВыполнения.РасчетДлины, ЭлементГруппы.Формат.Длина);
		
		ФорматироватьСтрочныйЭлемент(ПараметрыВыполненияРасчет, ЭлементДанных, ЭлементГруппы);
		
		НаборСтрок.Добавить(ЭлементГруппы.Формат.Результат);
	КонецЦикла;
	
	Результат = СоединитьСтрокиГоризонтально(НаборСтрок, " & ");
	ФорматированиеДобавитьРезультат(ЭлементДанных, Результат);
КонецПроцедуры

&НаКлиенте
Процедура ФорматироватьСтраницы(ПараметрыВыполнения, ЭлементДанных, Выполнена)
	Если НЕ ЭлементДанных.Тип = "Страницы" Тогда
		Возврат;
	КонецЕсли;
	Выполнена = Истина;
	
	ИсключаемыеСвойства = СтрРазделить(
		"Заголовок", Символы.ПС);
	
	ЭлементДанных.Формат.РазрывСтрок = Истина;
	
	Для Каждого Страница Из ЭлементДанных.Элементы Цикл
		Страница.Формат.Результат.Очистить();
		
		//ФорматированиеДобавитьРезультат(ЭлементДанных, "");
		Результат = "/" + Страница.НаборСвойств.Заголовок;
		Результат = Результат + ФорматироватьНаборСвойств(Страница.НаборСвойств, ИсключаемыеСвойства);
		ФорматированиеДобавитьРезультат(ЭлементДанных, Результат);
		//ФорматированиеДобавитьРезультат(ЭлементДанных, "");
		
		ПараметрыВыполненияРасчет = НовыеПараметрыФорматирования(ПараметрыВыполнения.РасчетДлины, Страница.Формат.Длина);
		ВыполнитьФорматирование(ПараметрыВыполненияРасчет, Страница);
		
		Результат = Новый Массив;
		Для Каждого Строка Из Страница.Формат.Результат Цикл
			Результат.Добавить("	" + Строка);
		КонецЦикла;
		
		ФорматированиеДобавитьРезультат(ЭлементДанных, Результат);
	КонецЦикла;
КонецПроцедуры

#КонецОбласти    

#Область ФорматироватьТаблицу

&НаКлиенте
Процедура ФорматироватьТаблицу(ПараметрыВыполнения, ЭлементДанных, Выполнена)
	Если НЕ ЭлементДанных.Тип = "Таблица" 
		И НЕ ЭлементДанных.Тип = "Дерево" Тогда
		Возврат;
	КонецЕсли;
	Выполнена = Истина;
	
	ЭлементДанных.Формат.Результат.Очистить(); 
	ЭлементДанных.Формат.РазрывСтрок = Истина;

	//ПустаяСтрока = СтрПовторитьСимвол(" ", ПараметрыВыполнения.Длина);

	//ФорматированиеДобавитьРезультат(ЭлементДанных, ПустаяСтрока);
	
	ИсключаемыеСвойства = Новый Массив;
	Результат = ФорматироватьНаборСвойств(ЭлементДанных.НаборСвойств, ИсключаемыеСвойства);
	
	Если НЕ ПустаяСтрока(Результат) Тогда
		ФорматированиеДобавитьРезультат(ЭлементДанных, Результат);
	КонецЕсли;

	ТаблицаЗаголовок = Новый Массив;
	ПараметрыЗаполнения = Новый Структура("ИндексКолонки, ИндексСтроки", 0, 0);
	ЗаполнитьЗаголовокТаблицыПредв(ПараметрыЗаполнения, ТаблицаЗаголовок, ЭлементДанных);
	ТаблицаЗаголовок = ДополнитьМассивНаборовКолонок(ТаблицаЗаголовок);
	
	СвернутаяТаблицаЗаголовок = СвернутьМассивНаборовКолонок(ТаблицаЗаголовок);
	
	ДлиныПоЯчейкам = Новый Соответствие;
	ЗаполнитьДлиныПоКолонкам(ДлиныПоЯчейкам, ЭлементДанных, 0);

	ТаблицаДлин = Новый Соответствие;                  
	ДлинаТаблицы = 0;
	ПолучитьТаблицуДлин(ТаблицаДлин, ДлиныПоЯчейкам, ЭлементДанных, ДлинаТаблицы);

	ЗаполнитьДлинуКолонокТаблицыСверхуВниз(ТаблицаДлин, ЭлементДанных);

	Таблица = СформироватьТаблицу(
		ЭлементДанных, 
		ТаблицаДлин, 
		ТаблицаЗаголовок, 
		СвернутаяТаблицаЗаголовок,
		Выполнена);
		
	Если НЕ Выполнена Тогда
		Возврат;
	КонецЕсли;
		
	РазделительЯчеек = Лексема("ТаблицаРазделитель");
	Для Каждого Строка Из Таблица Цикл
		Результат = РазделительЯчеек + СтрСоединить(Строка, РазделительЯчеек) + РазделительЯчеек;	
		ФорматированиеДобавитьРезультат(ЭлементДанных, Результат);
	КонецЦикла; 
КонецПроцедуры   

&НаКлиенте
Функция ДополнитьМассивНаборовКолонок(МассивНаборовКолонок)
	Результат = Новый Массив;
	Если МассивНаборовКолонок.Количество() = 0 Тогда
		Возврат Результат;
	КонецЕсли;
	
	КоличествоКолонок = МассивНаборовКолонок[0].Количество();
	
	ТекущийНабор = Новый Массив(КоличествоКолонок);
	
	Для Каждого НаборКолонок Из МассивНаборовКолонок Цикл
		НовыйНабор = Новый Массив;
		
		Для ИндексКолонки = 0 По НаборКолонок.ВГраница() Цикл
			ТекущаяКолонка = НаборКолонок[ИндексКолонки];
			
			Если ТекущаяКолонка = Неопределено Тогда
				ТекущаяКолонка = ТекущийНабор[ИндексКолонки];
			КонецЕсли;	
			
			НовыйНабор.Добавить(ТекущаяКолонка);
			ТекущийНабор[ИндексКолонки] = ТекущаяКолонка;
		КонецЦикла;	
		Результат.Добавить(НовыйНабор); 
	КонецЦикла;
	
	Возврат Результат;
КонецФункции

&НаКлиенте
Функция СвернутьМассивНаборовКолонок(МассивНаборовКолонок)
	Результат = Новый Массив;
	Если МассивНаборовКолонок.Количество() = 0 Тогда
		Возврат Результат;
	КонецЕсли;
	
	КоличествоКолонок = МассивНаборовКолонок[0].Количество();
	
	ТекущийНабор = Новый Массив(КоличествоКолонок);

	ИндексНабора = МассивНаборовКолонок.ВГраница();
	Пока ИндексНабора >= 0 Цикл   
		НаборКолонок = МассивНаборовКолонок[ИндексНабора];
		ИндексНабора = ИндексНабора - 1;
		
		НовыйНабор = Новый Массив;
		
		Для ИндексКолонки = 0 По НаборКолонок.ВГраница() Цикл
			ТекущаяКолонка = НаборКолонок[ИндексКолонки];
			Если ТекущаяКолонка = Неопределено Тогда
				Возврат Неопределено;
			КонецЕсли;	
			
			Если ТекущаяКолонка.Тип = "ГруппаКолонокТаблицы" Тогда
				ТекущаяКолонка = ТекущийНабор[ИндексКолонки];
			КонецЕсли;
			
			НовыйНабор.Добавить(ТекущаяКолонка);
			ТекущийНабор[ИндексКолонки] = ТекущаяКолонка;
		КонецЦикла;
		
		Результат.Вставить(0, НовыйНабор); 
	КонецЦикла;  
	
	УдалитьДублиИзМассиваНаборов(Результат);
	
	Возврат Результат;
КонецФункции 

&НаКлиенте
Процедура УдалитьДублиИзМассиваНаборов(МассивНаборовКолонок)
	ПрошлаяСтрока = Неопределено;    
	ИндексНабора = 0;
	Пока ИндексНабора <= МассивНаборовКолонок.ВГраница() Цикл
		ТекущаяСтрока = МассивНаборовКолонок[ИндексНабора];
		
		Если ПрошлаяСтрока <> Неопределено
			И СтрокиНабораРавны(ПрошлаяСтрока, ТекущаяСтрока) Тогда
			
			МассивНаборовКолонок.Удалить(ИндексНабора);
			Продолжить;
		КонецЕсли;
		
		ПрошлаяСтрока = ТекущаяСтрока;
		ИндексНабора = ИндексНабора + 1;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Функция СтрокиНабораРавны(Набор1, Набор2)
	Для Индекс = 0 По Набор1.ВГраница() Цикл
		Если Набор1[Индекс] <> Набор2[Индекс] Тогда
			Возврат Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Истина;
КонецФункции   

&НаКлиенте
Процедура УстановитьЗначениеВЯчейкеТаблицы(Таблица, ИндексКолонки, ИндексСтроки, Значение, ПустоеЗначение = "")
	Пока ИндексСтроки > Таблица.ВГраница() Цикл
		Таблица.Добавить(Новый Массив);
	КонецЦикла;

	КоличествоКолонок = ИндексКолонки + 1;
	Для Каждого Строка Из Таблица Цикл
		КоличествоКолонок = Макс(КоличествоКолонок, Строка.Количество());
	КонецЦикла;
	
	Для Каждого Строка Из Таблица Цикл
		Пока КоличествоКолонок > Строка.Количество() Цикл
			Строка.Добавить(ПустоеЗначение);
		КонецЦикла;
	КонецЦикла;
	
	Таблица[ИндексСтроки][ИндексКолонки] = Значение;
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьЗаголовокТаблицыПредв(ПараметрыЗаполнения, Таблица, ЭлементДанных)
	Если ЭлементДанных.Колонки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ТекИндексКолонки = ПараметрыЗаполнения.ИндексКолонки;
	ТекИндексСтроки = ПараметрыЗаполнения.ИндексСтроки;
	Для Каждого Колонка Из ЭлементДанных.Колонки Цикл
		НовыеПараметрыЗаполнения = Новый Структура("ИндексКолонки, ИндексСтроки", ТекИндексКолонки, ПараметрыЗаполнения.ИндексСтроки + 1);
		
		ЗаполнитьЗаголовокТаблицыПредв(НовыеПараметрыЗаполнения, Таблица, Колонка);
		
		НовыйИндексСтроки = Макс(НовыеПараметрыЗаполнения.ИндексСтроки, ТекИндексСтроки);
		НовыйИндексКолонки = НовыеПараметрыЗаполнения.ИндексКолонки;
		
		Для СчИндексКолонки = ТекИндексКолонки По НовыйИндексКолонки Цикл
			УстановитьЗначениеВЯчейкеТаблицы(Таблица, СчИндексКолонки, ПараметрыЗаполнения.ИндексСтроки, Колонка, Неопределено);
		КонецЦикла;
		
		ТекИндексСтроки = НовыйИндексСтроки;
		ТекИндексКолонки = НовыйИндексКолонки + 1;
	КонецЦикла;  
	
	ПараметрыЗаполнения.ИндексКолонки = ТекИндексКолонки - 1;
	ПараметрыЗаполнения.ИндексСтроки = ТекИндексСтроки;
КонецПроцедуры

&НаКлиенте
Функция МинимальнаяДлинаКолонкиТаблицы()
	Возврат 5;
КонецФункции

&НаКлиенте
Процедура ПолучитьТаблицуДлин(ПараметрыВыполнения, ДлиныПоЯчейкам, ЭлементДанных, ОбщаяДлина)
	ОбщаяДлина = 0;
	Для Каждого ОписаниеКолонки Из ЭлементДанных.Колонки Цикл
		Длина = СтрДлина(ФорматироватьКолонку(ОписаниеКолонки));
		
		Длина = Макс(МинимальнаяДлинаКолонкиТаблицы(), Длина);
		
		ДлинаЯчейки = ДлиныПоЯчейкам.Получить(ОписаниеКолонки.УИД);
		Если ДлинаЯчейки <> Неопределено Тогда
			Длина = Макс(Длина, ДлинаЯчейки);
		КонецЕсли;
		
		ДлинаНижестоящих = 0;
		ПолучитьТаблицуДлин(ПараметрыВыполнения, ДлиныПоЯчейкам, ОписаниеКолонки, ДлинаНижестоящих);
		
		Длина = Макс(ДлинаНижестоящих, Длина);
		
		ПараметрыВыполнения.Вставить(ОписаниеКолонки.УИД, Длина);
		
		ОбщаяДлина = ОбщаяДлина + Длина;
	КонецЦикла;
КонецПроцедуры      

&НаКлиенте
Процедура ЗаполнитьДлинуКолонокТаблицыСверхуВниз(ПараметрыВыполнения, ЭлементДанных)
	Для Каждого ОписаниеКолонки Из ЭлементДанных.Колонки Цикл
		Длина = ПараметрыВыполнения.Получить(ОписаниеКолонки.УИД);
		
		КоличествоКолонок = ОписаниеКолонки.Колонки.Количество();
		Если КоличествоКолонок = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		МассивЗначений = Новый Массив;
		Для Каждого Подколонка Из ОписаниеКолонки.Колонки Цикл
			МассивЗначений.Добавить(ПараметрыВыполнения.Получить(Подколонка.УИД));
		КонецЦикла;
		
 		МассивЗначений = РаспределитьЧислоСВыравниванием(Длина, МассивЗначений);
		
		Сч = 0;
		Для Каждого Подколонка Из ОписаниеКолонки.Колонки Цикл                                                                       
			ПараметрыВыполнения.Вставить(Подколонка.УИД, МассивЗначений[Сч]);
			Сч = Сч + 1;
		КонецЦикла;	
		
		ЗаполнитьДлинуКолонокТаблицыСверхуВниз(ПараметрыВыполнения, ОписаниеКолонки);
	КонецЦикла;
КонецПроцедуры 

&НаКлиенте
Функция РаспределитьЧислоСВыравниванием(ЧислоДляРаспределения, МассивЗначений)
	МассивРезультат = Новый Массив;
	
	Сумма = 0;
	Для каждого ЗначениеИзМассива Из МассивЗначений Цикл
		Сумма = Сумма + ЗначениеИзМассива;
	КонецЦикла;
	
	Если Сумма >= ЧислоДляРаспределения Тогда
		Возврат МассивЗначений;
	КонецЕсли;
	
	СреднееАрифметическое = Цел(ЧислоДляРаспределения / МассивЗначений.Количество());
	
	// Определение "максимальных" значений
	МассивМаксимальныхИндексов = Новый Массив;
	МассивМинимальныхЗначений = Новый Массив;
	Для Индекс = 0 По МассивЗначений.Количество() - 1 Цикл
		Если МассивЗначений[Индекс] >= СреднееАрифметическое Тогда
			МассивМаксимальныхИндексов.Добавить(Индекс);
			МассивРезультат.Добавить(МассивЗначений[Индекс]); // Добавляем сразу значения без изменений
		Иначе
			МассивМинимальныхЗначений.Добавить(Индекс);
			МассивРезультат.Добавить(0); // Заполним нулями для начала
		КонецЕсли;
	КонецЦикла;
	
	// Распределение числа на "минимальные" значения
	Остаток = ЧислоДляРаспределения;
	КолМинимальныхЗначений = МассивМинимальныхЗначений.Количество();
	
	Если КолМинимальныхЗначений = 0 Тогда
		Возврат МассивРезультат;	
	КонецЕсли;
	
	ДоляРаспределения = Цел(Остаток / КолМинимальныхЗначений);
	
	Для каждого ИндексИзМассива Из МассивМинимальныхЗначений Цикл
		МассивРезультат[ИндексИзМассива] = ДоляРаспределения;
		Остаток = Остаток - ДоляРаспределения;
		
		Если Остаток = 0 Тогда
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если Остаток > 0 И КолМинимальныхЗначений > 0 Тогда
		ИндексИзМассива = МассивМинимальныхЗначений[0];
		МассивРезультат[ИндексИзМассива] = МассивРезультат[ИндексИзМассива] + Остаток;
	КонецЕсли;  
	
	Возврат МассивРезультат;
КонецФункции

&НаКлиенте
Процедура ЗаполнитьДлиныПоКолонкам(ПараметрыВыполнения, ОписаниеТаблицы, Уровень)	
	Для Каждого Строка Из ОписаниеТаблицы.Строки Цикл
		Для Каждого Ячейка Из Строка.Ячейки Цикл
			ТекущаяДлина = ПараметрыВыполнения.Получить(Ячейка.УИДКолонки);
			Если ТекущаяДлина = Неопределено Тогда
				ТекущаяДлина = 0;
			КонецЕсли;
			ПараметрыВыполнения.Вставить(Ячейка.УИДКолонки, Макс(ТекущаяДлина, СтрДлина(ФорматироватьЯчейку(Ячейка, Уровень))));
		КонецЦикла;
		
		ЗаполнитьДлиныПоКолонкам(ПараметрыВыполнения, Строка, Уровень + 1);
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Функция ФорматироватьКолонку(Колонка, Длина = 0) 
	ИсключаемыеСвойства = Новый Массив;
	ИсключаемыеСвойства.Добавить("Заголовок");
	
	Если Колонка.НаборСвойств.Свойство("ГоризонтальноеПоложение") Тогда
		ТекГоризонтальноеПоложение	= Колонка.НаборСвойств.ГоризонтальноеПоложение;
	Иначе
		ТекГоризонтальноеПоложение	= "Лево";
	КонецЕсли;
	
	Если ГоризонтальноеПоложение = "Лево" Тогда
		ИсключаемыеСвойства.Добавить("ГоризонтальноеПоложение");
	КонецЕсли;    
	
	Если Колонка.Колонки.Количество() = 0 Тогда
		ИсключаемыеСвойства.Добавить("ГоризонтальноеПоложение");
	КонецЕсли;
	
	Группа = Лексема("ТаблицаКолонкаГруппа");
	Описание = Колонка.НаборСвойств.Заголовок;
	Если Колонка.Тип = "ГруппаКолонокТаблицы" Тогда
		Описание =  Группа + Колонка.НаборСвойств.Заголовок + Группа;
	КонецЕсли; 
	
	Описание = Описание + ФорматироватьНаборСвойств(Колонка.НаборСвойств, ИсключаемыеСвойства, Колонка.ОписаниеТипов);
	
	Описание = " " + Описание + " ";
	Описание = ВыровнитьСтроку(Описание, ТекГоризонтальноеПоложение, Длина);
	
	Возврат Описание;
КонецФункции

&НаКлиенте
Функция ФорматироватьРазделитель(Длина, ГоризонтальноеПоложение)
	
	СимволСлева = " ";
	СимволСправа = " ";
	
	Если ГоризонтальноеПоложение = "Право" Тогда
		СимволСправа = ": ";
	КонецЕсли;
	
	Если ГоризонтальноеПоложение = "Центр" Тогда
		СимволСлева = " :";
		СимволСправа = ": ";
	КонецЕсли;
	
	Строка = СимволСлева 
		+ СтрПовторитьСимвол("-", Длина - СтрДлина(СимволСлева) - СтрДлина(СимволСправа))
		+ СимволСправа;
		
	Возврат Строка;
КонецФункции

&НаКлиенте
Функция ФорматироватьЯчейку(Ячейка, Уровень, Колонка = Неопределено, Длина = Неопределено)      
	Строка = Ячейка.Значение;
	
	Строка = ПолучитьСтрокуФлажок(
		Строка, 
		Ячейка.ЕстьФлажок, 
		"Авто",
		Ячейка.ЗначениеФлажка, 
		"Право");

	Если Уровень > 0 Тогда
		Строка = СтрПовторитьСимвол(".", Уровень) + " " + Строка;
	КонецЕсли;
		
	Строка = " " + Строка + " ";
	
	Если Колонка <> Неопределено Тогда     
	
		Если Колонка.НаборСвойств.Свойство("ГоризонтальноеПоложение") Тогда
			ТекГоризонтальноеПоложение	= Колонка.НаборСвойств.ГоризонтальноеПоложение;
		Иначе
			ТекГоризонтальноеПоложение	= "Лево";
		КонецЕсли;
		
		Строка = ВыровнитьСтроку(Строка, ТекГоризонтальноеПоложение, Длина);
	КонецЕсли;
	
	Возврат Строка;
КонецФункции

&НаКлиенте
Функция СформироватьТаблицу(ОписаниеДанных, ТаблицаДлин, ТаблицаЗаголовки, ТаблицаСвернутыеЗаголовки, Выполнена)
	Таблица = Новый Массив;
	
	СформироватьЗаголовкиТаблицы(Таблица, ТаблицаДлин, ТаблицаЗаголовки);

	Если ТаблицаЗаголовки.Количество() = 0 Тогда
		Выполнена = Ложь;
		Возврат Выполнена;
	КонецЕсли;

	ИндексСтроки = ТаблицаЗаголовки.ВГраница() + 1;	
	
	СформироватьРазделительТаблицы(Таблица, ТаблицаДлин, ТаблицаЗаголовки, ИндексСтроки);
	
	ИндексСтроки = ИндексСтроки + 1;	
	
	СформироватьСтрокиТаблицы(ОписаниеДанных, Таблица, ТаблицаДлин, ТаблицаСвернутыеЗаголовки, ИндексСтроки, 0);
	
	Возврат Таблица;
КонецФункции

&НаКлиенте
Процедура СформироватьЗаголовкиТаблицы(Таблица, ТаблицаДлин, ТаблицаЗаголовки)
	МассивКолонок = Новый Массив;
	
	Для ИндексСтроки = 0 По ТаблицаЗаголовки.ВГраница() Цикл
		МассивКолонокПоСтроке = Новый Массив;
		
		ЗаголовкиСтрока = ТаблицаЗаголовки[ИндексСтроки];
		Для ИндексКолонки = 0 По ЗаголовкиСтрока.ВГраница() Цикл
			Колонка = ЗаголовкиСтрока[ИндексКолонки];
			
			Длина = ТаблицаДлин.Получить(Колонка.УИД);
			
			ЭтоПовторПоСтроке = Ложь;
			ЭтоПовторПоКолонке = Ложь;
			УстановитьПризнакПовтораДляКолонкиТаблицы(МассивКолонок, МассивКолонокПоСтроке, Колонка, ЭтоПовторПоКолонке, ЭтоПовторПоСтроке);
			
			Если ЭтоПовторПоСтроке Тогда
				Значение = "";
			ИначеЕсли ЭтоПовторПоКолонке Тогда
				Значение = СтрПовторитьСимвол(" ", Длина);
			Иначе
				Значение = ФорматироватьКолонку(Колонка, Длина);
			КонецЕсли;
			
			УстановитьЗначениеВЯчейкеТаблицы(Таблица, ИндексКолонки, ИндексСтроки, Значение, "");
		КонецЦикла;
	КонецЦикла;   

КонецПроцедуры 

&НаКлиенте
Процедура СформироватьРазделительТаблицы(Таблица, ТаблицаДлин, ТаблицаЗаголовки, ИндексСтроки)
	ПоследнийЗаголовок = ТаблицаЗаголовки[ТаблицаЗаголовки.ВГраница()];
	
	Для ИндексКолонки = 0 По ПоследнийЗаголовок.ВГраница() Цикл
		Колонка = ПоследнийЗаголовок[ИндексКолонки];
		Длина = ТаблицаДлин.Получить(Колонка.УИД);
		
		Если Колонка.НаборСвойств.Свойство("ГоризонтальноеПоложение") Тогда
			ТекГоризонтальноеПоложение	= Колонка.НаборСвойств.ГоризонтальноеПоложение;
		Иначе
			ТекГоризонтальноеПоложение	= "Лево";
		КонецЕсли;
		
		Значение = ФорматироватьРазделитель(Длина, ТекГоризонтальноеПоложение);
		
		УстановитьЗначениеВЯчейкеТаблицы(Таблица, ИндексКолонки, ИндексСтроки, Значение, "");
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьСтрокиТаблицы(ОписаниеДанных, Таблица, ТаблицаДлин, ТаблицаСвернутыеЗаголовки, ИндексСтроки, Уровень) 
	Для Каждого Строка Из ОписаниеДанных.Строки Цикл
		СформироватьСтрокуТаблицы(Строка, Таблица, ТаблицаДлин, ТаблицаСвернутыеЗаголовки, ИндексСтроки, Уровень);
		
		ИндексСтроки = ИндексСтроки + ТаблицаСвернутыеЗаголовки.Количество();
		
		СформироватьСтрокиТаблицы(Строка, Таблица, ТаблицаДлин, ТаблицаСвернутыеЗаголовки, ИндексСтроки, Уровень + 1);
	КонецЦикла;
КонецПроцедуры   

&НаКлиенте
Процедура СформироватьСтрокуТаблицы(Строка, Таблица, ТаблицаДлин, ТаблицаСвернутыеЗаголовки, НачальныйИндексСтроки, Уровень) 
	МассивКолонок = Новый Массив;
	
	ЯчейкиПоКолонкам = ПолучитьСоответствиеЯчеекПоКолонкам(Строка.Ячейки);
	
	Для ИндексСтроки = 0 По ТаблицаСвернутыеЗаголовки.ВГраница() Цикл 
		МассивКолонокПоСтроке = Новый Массив;
		
		ЗаголовкиСтрока = ТаблицаСвернутыеЗаголовки[ИндексСтроки];
		Для ИндексКолонки = 0 По ЗаголовкиСтрока.ВГраница() Цикл
			Колонка = ЗаголовкиСтрока[ИндексКолонки];
			
			ЭтоПовторПоСтроке = Ложь;
			ЭтоПовторПоКолонке = Ложь;
			УстановитьПризнакПовтораДляКолонкиТаблицы(МассивКолонок, МассивКолонокПоСтроке, Колонка, ЭтоПовторПоКолонке, ЭтоПовторПоСтроке);
			
			Длина = ТаблицаДлин.Получить(Колонка.УИД);
			
			Ячейка = ЯчейкиПоКолонкам.Получить(Колонка.УИД);
			
			Значение = СформироватьЗначениеЯчейкиТаблицы(Ячейка, Длина, Колонка, ИндексКолонки, Уровень, ЭтоПовторПоКолонке, ЭтоПовторПоСтроке);
			
			УстановитьЗначениеВЯчейкеТаблицы(Таблица, ИндексКолонки, НачальныйИндексСтроки + ИндексСтроки, Значение, "");
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Функция СформироватьЗначениеЯчейкиТаблицы(Ячейка, Длина, Колонка, ИндексКолонки, Уровень, ЭтоПовторПоКолонке, ЭтоПовторПоСтроке)
	Если ЭтоПовторПоСтроке Тогда
		Возврат "";
	КонецЕсли;	
	
	Если Ячейка = Неопределено ИЛИ ЭтоПовторПоКолонке Тогда 
		Если ИндексКолонки <> 0 Тогда
			Возврат СтрПовторитьСимвол(" ", Длина);
		КонецЕсли;
		
		Значение = " " + ПолучитьОписаниеСУровнем("", Уровень);
		Возврат Значение + СтрПовторитьСимвол(" ", Длина - СтрДлина(Значение));
	КонецЕсли;
	
	ТекУровень = ?(ИндексКолонки = 0, Уровень, 0);
	Значение = ФорматироватьЯчейку(Ячейка, ТекУровень, Колонка, Длина);
	Возврат Значение;
КонецФункции

&НаКлиенте
Процедура УстановитьПризнакПовтораДляКолонкиТаблицы(МассивКолонок, МассивКолонокПоСтроке, Колонка, ЭтоПовторПоКолонке, ЭтоПовторПоСтроке)
	Если МассивКолонокПоСтроке.Найти(Колонка) <> Неопределено Тогда
		ЭтоПовторПоСтроке = Истина;
	КонецЕсли;
	МассивКолонокПоСтроке.Добавить(Колонка);
	
	Если НЕ ЭтоПовторПоСтроке И МассивКолонок.Найти(Колонка) <> Неопределено Тогда
		ЭтоПовторПоКолонке = Истина;
	КонецЕсли;
	МассивКолонок.Добавить(Колонка);
КонецПроцедуры

&НаКлиенте
Функция ПолучитьСоответствиеЯчеекПоКолонкам(Ячейки)
	ЯчейкиПоКолонкам = Новый Соответствие;
	Для Каждого Ячейка Из Ячейки Цикл
		ЯчейкиПоКолонкам.Вставить(Ячейка.УИДКолонки, Ячейка);
	КонецЦикла;
	Возврат ЯчейкиПоКолонкам;
КонецФункции

#КонецОбласти

#Область ФорматироватьПолеВвода

&НаКлиенте
Процедура ФорматироватьПолеВвода(ПараметрыВыполнения, ЭлементДанных, Выполнена)
	Если НЕ ЭлементДанных.Тип = "ПолеВвода" Тогда
		Возврат;
	КонецЕсли;
	Выполнена = Истина;
	
	ИсключаемыеСвойства = СтрРазделить(
		"КнопкаВыпадающегоСписка
		|КнопкаВыбора
		|КнопкаОчистки
		|КнопкаРегулирования
		|КнопкаОткрытия
		|ГоризонтальноеПоложениеВГруппе
		|Заголовок", Символы.ПС);
	
	ЗаголовокЭлемента = ЭлементДанных.НаборСвойств.Заголовок + Лексема("ПолеВводаРазделитель") + " ";
	Результат = ЗаголовокЭлемента + ЭлементДанных.Значение;

	ДлинаЗначения = СтрДлина(ЭлементДанных.Значение);
	ДлинаЗаголовка = СтрДлина(ЗаголовокЭлемента);
		
	СвойстваПоляВвода = "";   
	ФорматироватьДобавитьСвойство(СвойстваПоляВвода, ЭлементДанных.НаборСвойств, "КнопкаВыбора", "В");
	ФорматироватьДобавитьСвойство(СвойстваПоляВвода, ЭлементДанных.НаборСвойств, "КнопкаВыпадающегоСписка", "С");
	ФорматироватьДобавитьСвойство(СвойстваПоляВвода, ЭлементДанных.НаборСвойств, "КнопкаОчистки", "Х");
	ФорматироватьДобавитьСвойство(СвойстваПоляВвода, ЭлементДанных.НаборСвойств, "КнопкаОткрытия", "О");
	ФорматироватьДобавитьСвойство(СвойстваПоляВвода, ЭлементДанных.НаборСвойств, "КнопкаРегулирования", "Д");
	
	Если НЕ ПустаяСтрока(СвойстваПоляВвода) Тогда
		Результат = Результат + "__" + СвойстваПоляВвода;
	КонецЕсли;
	
	ЭтоМногострочнаяСтрока = 
		ЭлементДанных.НаборСвойств.Свойство("МногострочныйРежим") 
		И Булево(ЭлементДанных.НаборСвойств.МногострочныйРежим)
		И ЭлементДанных.НаборСвойств.Свойство("Высота")
		И Число(ЭлементДанных.НаборСвойств.Высота) > 1;
	
	Если ЭтоМногострочнаяСтрока Тогда
		ИсключаемыеСвойства.Добавить("МногострочныйРежим");
		ИсключаемыеСвойства.Добавить("Высота");
	КонецЕсли;
	
	Результат = Результат + ФорматироватьНаборСвойств(ЭлементДанных.НаборСвойств, ИсключаемыеСвойства, ЭлементДанных.ОписаниеТипов);
	
	Результат = ФорматироватьГоризонтальноеПоложение(
		Результат, 
		ЭлементДанных.НаборСвойств, 
		ПараметрыВыполнения.Длина);
	
	ФорматированиеУстановитьРезультат(ЭлементДанных, Результат);	

	Если ЭтоМногострочнаяСтрока Тогда
		ТекВысота = Число(ЭлементДанных.НаборСвойств.Высота);
		
		СтрМногострочный = СтрПовторитьСимвол(" ", ДлинаЗаголовка) +  СтрПовторитьСимвол("_", ДлинаЗначения);
		
		Для СчСтрок = 2 По ТекВысота Цикл // BSLLS:UnusedLocalVariable-off  
			ФорматированиеДобавитьРезультат(ЭлементДанных, СтрМногострочный);
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти    

#Область ФорматироватьНадпись

&НаКлиенте
Процедура ФорматироватьНадпись(ПараметрыВыполнения, ЭлементДанных, Выполнена)
	Если НЕ ЭлементДанных.Тип = "Надпись" Тогда
		Возврат;
	КонецЕсли;
	Выполнена = Истина;
	
	ИсключаемыеСвойства = СтрРазделить(
		"ГоризонтальноеПоложениеВГруппе
		|Заголовок", Символы.ПС);
	
	Результат = ЭлементДанных.НаборСвойств.Заголовок;
	
	Результат = Результат + ФорматироватьНаборСвойств(ЭлементДанных.НаборСвойств, ИсключаемыеСвойства);
	
	Результат = ФорматироватьГоризонтальноеПоложение(
		Результат, 
		ЭлементДанных.НаборСвойств, 
		ПараметрыВыполнения.Длина);
	
	ФорматированиеУстановитьРезультат(ЭлементДанных, Результат);	
КонецПроцедуры

#КонецОбласти    

#Область ФорматироватьФлажок

&НаКлиенте
Процедура ФорматироватьФлажок(ПараметрыВыполнения, ЭлементДанных, Выполнена)
	Если НЕ ЭлементДанных.Тип = "ПолеФлажка" Тогда
		Возврат;
	КонецЕсли;
	Выполнена = Истина;
	
	ИсключаемыеСвойства = СтрРазделить(
		"Заголовок
		|ГоризонтальноеПоложениеВГруппе
		|ПоложениеЗаголовка
		|ВидФлажка", Символы.ПС);
	
	Результат = ПолучитьЗначениеСвойства(ЭлементДанных.НаборСвойств, "Заголовок");
	
	Результат = ПолучитьСтрокуФлажок(
		Результат, 
		Истина,
		ПолучитьЗначениеСвойства(ЭлементДанных.НаборСвойств, "ВидФлажка"),
		ЭлементДанных.Значение, 
		ПолучитьЗначениеСвойства(ЭлементДанных.НаборСвойств, "ПоложениеЗаголовка", "Право"));
	
	Результат = Результат + ФорматироватьНаборСвойств(ЭлементДанных.НаборСвойств, ИсключаемыеСвойства);
	
	Результат = ФорматироватьГоризонтальноеПоложение(
		Результат, 
		ЭлементДанных.НаборСвойств, 
		ПараметрыВыполнения.Длина);
	
	ФорматированиеУстановитьРезультат(ЭлементДанных, Результат);
КонецПроцедуры

&НаКлиенте
Функция ПолучитьСтрокуФлажок(Строка, ЕстьФлажок, ВидФлажка, ЗначениеФлажка, ПоложениеЗаголовка)
	Если НЕ ЕстьФлажок Тогда
		Возврат Строка;
	КонецЕсли;
	
	Если ВидФлажка = "Выключатель" Тогда
		Если ЗначениеФлажка Тогда
			Значение = " |1";
		Иначе
			Значение = "0| ";
		КонецЕсли;
	Иначе
		Значение = " ";
		Если ЗначениеФлажка Тогда
			Значение = "X";
		КонецЕсли;
	КонецЕсли;
	
	Значение = "[" + Значение + "]";
	
	Если ПустаяСтрока(Строка) Тогда
		Возврат Значение;
	КонецЕсли;
	
	Если ПоложениеЗаголовка = "Право" Тогда
		Возврат Значение + " " + Строка;
	КонецЕсли;
	Возврат Строка + " " + Значение;
КонецФункции

#КонецОбласти    

#Область ФорматироватьКоманднуюПанель

&НаКлиенте
Процедура ФорматироватьКоманднуюПанель(ПараметрыВыполнения, ЭлементДанных, Выполнена)
	Если НЕ ЭлементДанных.Тип = "КоманднаяПанель" Тогда
		Возврат;
	КонецЕсли;
	Выполнена = Истина;
	
	ИсключаемыеСвойства = СтрРазделить(
		"ГоризонтальноеПоложениеВГруппе", Символы.ПС);
	
	ЕстьМеню = Ложь;
	
	Кнопки = ПолучитьСписокКнопокБезГрупп(ЭлементДанных);
	
	НаборКнопок = ФорматироватьПолучитьКнопкиКоманднойПанели(ПараметрыВыполнения, Кнопки, ЕстьМеню);
	
	СтрКнопки = СтрСоединить(
		НаборКнопок, 
		" " +  Лексема("КоманднаяПанельРазделительКнопок") + " ");

	Результат = Лексема("КоманднаяПанельНачало") 
		+ " " 
		+ СтрКнопки
		+ " "; 
	
	Если НЕ ЕстьМеню Тогда
		Результат = Результат + Лексема("КоманднаяПанельКонец"); 
	
		Результат = Результат + ФорматироватьНаборСвойств(ЭлементДанных.НаборСвойств, ИсключаемыеСвойства);
		
		Результат = ФорматироватьГоризонтальноеПоложение(
			Результат, 
			ЭлементДанных.НаборСвойств, 
			ПараметрыВыполнения.Длина);
		
		ФорматированиеУстановитьРезультат(ЭлементДанных, Результат);	
		
		Возврат;
	КонецЕсли;
	
	ПараметрыФорматированияГоризонтальногоПоложения = ФорматироватьГоризонтальноеПоложениеПодготовка(
		Результат, 
		ЭлементДанных.НаборСвойств, 
		ПараметрыВыполнения.Длина);
		
	Результат = 
		ПараметрыФорматированияГоризонтальногоПоложения.Слева 
		+ ПараметрыФорматированияГоризонтальногоПоложения.Строка;
		
	ФорматированиеУстановитьРезультат(ЭлементДанных, Результат);	
	
	Для Каждого Кнопка Из Кнопки Цикл
		Если Кнопка.Тип <> "Подменю" Тогда
			Продолжить;
		КонецЕсли;	
		
		ФорматироватьМеню(
			ПараметрыВыполнения, 
			ЭлементДанных, 
			Кнопка, 
			0);
	КонецЦикла;
	
	ТекстКонецЭлемента = 
		" " 
		+ Лексема("КоманднаяПанельКонец") 
		+ ПараметрыФорматированияГоризонтальногоПоложения.Справа;
	ФорматированиеДобавитьРезультат(ЭлементДанных, ТекстКонецЭлемента, Истина);	
КонецПроцедуры

&НаКлиенте
Функция ФорматироватьПолучитьКнопкиКоманднойПанели(ПараметрыВыполнения, Кнопки, ЕстьМеню)
	НаборКнопок = Новый Массив;
	Для Каждого Кнопка Из Кнопки Цикл
		Если Кнопка.Тип = "ГруппаКнопок" Тогда
			НаборКнопок.Добавить(" " + Лексема("КоманднаяПанельРазделительМеню"));
			Продолжить;
		КонецЕсли;
		
		Если Кнопка.Тип = "Подменю" Тогда
			ЕстьМеню = Истина;
		КонецЕсли;
		
		ПараметрыВыполненияКнопка = НовыеПараметрыФорматирования(ПараметрыВыполнения.РасчетДлины, ПараметрыВыполнения.Длина);
		ФорматироватьКнопкуКоманднойПанели(ПараметрыВыполненияКнопка, Кнопка);
		
		НаборКнопок.Добавить(Кнопка.Формат.Результат[0]);
	КонецЦикла;
	
	Возврат НаборКнопок;
КонецФункции

&НаКлиенте
Процедура ФорматироватьМеню(
		ПараметрыВыполнения, 
		ТекКоманднаяПанель, 
		ЭлементДанных, 
		Уровень)
		
	Если ЭлементДанных.Тип = "ГруппаКнопок" Тогда
		Результат = Лексема("КоманднаяПанельРазделительМеню");
	Иначе
		ЭтоМеню = (Уровень = 0);	
		Результат = ПолучитьФорматированноеОписаниеКнопки(ЭлементДанных, ЭтоМеню);
	КонецЕсли;

	Результат = ПолучитьОписаниеСУровнем(Результат, Уровень);
	
	ФорматированиеДобавитьРезультат(ТекКоманднаяПанель, Результат);	
	
	Если ЭлементДанных.Тип = "Подменю" Тогда
		ФорматироватьЭлементыМеню(ПараметрыВыполнения, ТекКоманднаяПанель, Уровень + 1, ЭлементДанных);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ФорматироватьЭлементыМеню(ПараметрыВыполнения, ТекКоманднаяПанель, Уровень, Меню)
	ЭлементыБезГрупп = ПолучитьСписокКнопокБезГрупп(Меню);
	Для Каждого Подменю Из ЭлементыБезГрупп Цикл
		ФорматироватьМеню(
			ПараметрыВыполнения, 
			ТекКоманднаяПанель, 
			Подменю, 
			Уровень);
	КонецЦикла;	
КонецПроцедуры

&НаКлиенте
Функция ПолучитьФорматированноеОписаниеКнопки(ЭлементДанных, ЭтоМеню = Ложь)
	
	ТекстКартинка = ПолучитьЗначениеСвойства(ЭлементДанных.НаборСвойств, "Картинка");

	ТекстЗаголовок = ПолучитьЗначениеСвойства(ЭлементДанных.НаборСвойств, "Заголовок");
	
	Если ТекстКартинка <> Неопределено Тогда
		ТекстКартинка = "@" + ТекстКартинка;
	КонецЕсли;

	Если ЭтоМеню И НЕ ПустаяСтрока(ТекстЗаголовок) Тогда
		ТекстКартинка = Неопределено;
	КонецЕсли;
	
	Если ТекстКартинка = Неопределено Тогда
		Возврат ТекстЗаголовок;
	КонецЕсли;

	ПоложениеКартинки = "Лево";
	Если ЭлементДанных.НаборСвойств.Свойство("ПоложениеКартинки") Тогда
		ПоложениеКартинки = ЭлементДанных.НаборСвойств.ПоложениеКартинки;
	КонецЕсли;
	
	Если ПоложениеКартинки = "Лево" Тогда
		Возврат ТекстКартинка + " " + ТекстЗаголовок;
	КонецЕсли;
	
	Возврат ТекстЗаголовок  + " " + ТекстКартинка;
КонецФункции

&НаКлиенте
Процедура ФорматироватьКнопкуКоманднойПанели(ПараметрыВыполнения, ЭлементДанных)
	ИсключаемыеСвойства = СтрРазделить(
		"ПоложениеКартинки
		|Картинка
		|Заголовок", Символы.ПС);
	
	Результат = ПолучитьФорматированноеОписаниеКнопки(ЭлементДанных);
	
	Результат = Результат + ФорматироватьНаборСвойств(ЭлементДанных.НаборСвойств, ИсключаемыеСвойства);
	
	ФорматированиеУстановитьРезультат(ЭлементДанных, Результат);	
КонецПроцедуры

&НаКлиенте
Функция ПолучитьОписаниеСУровнем(Строка, Уровень)
	Если Уровень = 0 Тогда
		Возврат Строка;
	КонецЕсли;	
	
	Результат = СтрПовторитьСимвол(Лексема("Уровень"), Уровень);
	
	Если НЕ ПустаяСтрока(Результат) Тогда
		Результат = Результат + " " + Строка;
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

&НаКлиенте
Функция ПолучитьСписокКнопокБезГрупп(Родитель)
	Результат = Новый Массив;
	Для Каждого Кнопка Из Родитель.Элементы Цикл
		Если Кнопка.Тип <> "ГруппаКнопок" Тогда
			Результат.Добавить(Кнопка);
			Продолжить;
		КонецЕсли;
			
		НаборКнопок = ПолучитьСписокКнопокБезГрупп(Кнопка);
		
		Если Результат.Количество() > 0 Тогда
			Результат.Добавить(Кнопка);
		КонецЕсли; 
		
		Для Каждого ДобавляемаяКнопка Из НаборКнопок Цикл
			Результат.Добавить(ДобавляемаяКнопка);
		КонецЦикла;
	КонецЦикла;
	
	Возврат Результат;
КонецФункции

#КонецОбласти

&НаКлиенте
Процедура ФорматироватьДобавитьСвойство(Строка, Свойства, ИмяСвойство, Значение)   
	Если НЕ Свойства.Свойство(ИмяСвойство) Тогда
		Возврат;
	КонецЕсли;
	Если НЕ Свойства[ИмяСвойство] Тогда
		Возврат;
	КонецЕсли;
	Строка = Строка + Значение;
КонецПроцедуры

&НаКлиенте
Функция ФорматироватьНаборСвойств(Свойства, ИсключаемыеСвойства, ОписаниеТипов = Неопределено)  
	Шаблон = "%1 = %2";
	МассивРезультат = Новый Массив;
	
	Если ОписаниеТипов <> Неопределено Тогда
		ПредставлениеОписаниеТипов = ФорматироватьОписаниеТипов(ОписаниеТипов);
		Если НЕ ПустаяСтрока(ПредставлениеОписаниеТипов) Тогда
			СтрокаСвойство = СтрШаблон(Шаблон, "Тип", ПредставлениеОписаниеТипов);
			МассивРезультат.Добавить(СтрокаСвойство);
		КонецЕсли;
	КонецЕсли;
	
	Для Каждого КлючЗначение Из Свойства Цикл 
		Если ИсключаемыеСвойства.Найти(КлючЗначение.Ключ) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;   
		
		Если ТипЗнч(КлючЗначение.Значение) = Тип("Массив") Тогда
			Значение = СтрСоединить(КлючЗначение.Значение, ", ");
		Иначе
			Значение = СокрЛП(КлючЗначение.Значение);
		КонецЕсли;
		
		СтрокаСвойство = СтрШаблон("%1 = %2", КлючЗначение.Ключ, Значение);
		МассивРезультат.Добавить(СтрокаСвойство);
	КонецЦикла;  
	
	Если МассивРезультат.Количество() = 0 Тогда
		Возврат "";
	КонецЕсли;
	
	Возврат " {" + СтрСоединить(МассивРезультат, "; ") + "}";
КонецФункции

&НаКлиенте
Функция ПолучитьФорматированиеТипа(НазваниеТипа, ПараметрыТипа)
	Результат = НазваниеТипа;
	Если ПараметрыТипа.Количество() = 0 Тогда
		Возврат Результат;
	КонецЕсли;
	Возврат Результат + "(" + СтрСоединить(ПараметрыТипа,", ") + ")";
КонецФункции

&НаКлиенте
Функция СоединитьСтрокиГоризонтально(
	НаборыСтрок, 
	Символ, 
	СимволПерваяСтрока = Неопределено, 
	ДобавитьВНачалоКонец = Ложь)

	Результат = Новый Массив;
	СчСтроки = 0;
	Пока Истина Цикл
		МассивСтрок = Новый Массив;
		ДобавленаСтрока = Ложь;
		Для Каждого Набор Из НаборыСтрок Цикл
			Если СчСтроки > Набор.ВГраница() Тогда
				МассивСтрок.Добавить("");
				Продолжить;
			КонецЕсли;
			МассивСтрок.Добавить(Набор[СчСтроки]);
			ДобавленаСтрока = Истина;
		КонецЦикла;    
		
		Если НЕ ДобавленаСтрока Тогда
			Прервать;
		КонецЕсли;
		
		ТекСимвол = Символ;
		Если СчСтроки = 0 И СимволПерваяСтрока <> Неопределено Тогда
			ТекСимвол = СимволПерваяСтрока;
		КонецЕсли;

        СтрРезультат = СтрСоединить(МассивСтрок, ТекСимвол);
		Если ДобавитьВНачалоКонец Тогда
			СтрРезультат  = ТекСимвол + СтрРезультат + ТекСимвол;
		КонецЕсли; 
		
		Результат.Добавить(СтрРезультат);
		СчСтроки = СчСтроки + 1;
	КонецЦикла;     

	Возврат Результат;
КонецФункции

&НаКлиенте
Функция СтрПовторитьСимвол(Символ, Количество)
	Если Количество <= 0 Тогда
		Возврат "";
	КонецЕсли;
	
	Результат = "";
	Для Сч = 1 По Количество Цикл // BSLLS:UnusedLocalVariable-off
		Результат = Результат + Символ;
	КонецЦикла;
	Возврат Результат;
КонецФункции

&НаКлиенте
Функция ВыровнитьСтроку(Знач Строка, ГоризонтальноеВыравнивание, Длина)
	КоличествоСимволовКДобавлению = Длина - СтрДлина(Строка);
	Если КоличествоСимволовКДобавлению <= 0 Тогда
		Возврат Строка;
	КонецЕсли;
	
	Если ГоризонтальноеВыравнивание = "Лево" Тогда
		Возврат Строка + СтрПовторитьСимвол(" ", КоличествоСимволовКДобавлению);
	КонецЕсли;
	
	Если ГоризонтальноеВыравнивание = "Право" Тогда
		Возврат СтрПовторитьСимвол(" ", КоличествоСимволовКДобавлению) + Строка;
	КонецЕсли;

	КоличествоСимволовКДобавлениюСлева = Цел(КоличествоСимволовКДобавлению / 2);
	КоличествоСимволовКДобавлениюСправа = КоличествоСимволовКДобавлению - КоличествоСимволовКДобавлениюСлева;
	
	Возврат СтрПовторитьСимвол(
		" ", 
		КоличествоСимволовКДобавлениюСлева) + Строка + СтрПовторитьСимвол(" ", КоличествоСимволовКДобавлениюСправа);
КонецФункции

&НаКлиенте
Процедура ФорматированиеУстановитьРезультат(ЭлементДанных, Результат)
	ФорматированиеДобавитьРезультат(ЭлементДанных, Результат);
КонецПроцедуры

&НаКлиенте
Процедура ФорматированиеЗаменитьРезультат(ЭлементДанных, Результат, Индекс)
	
КонецПроцедуры

&НаКлиенте
Процедура ФорматированиеДобавитьРезультат(ЭлементДанных, Результат, ДобавитьВПоследнююСтроку = Ложь)
	Если ДобавитьВПоследнююСтроку Тогда
		НаборСтрок = ЭлементДанных.Формат.Результат;
		Индекс = НаборСтрок.ВГраница();
		ПоследняяСтрока = НаборСтрок[Индекс];
		НаборСтрок[Индекс] = ПоследняяСтрока + Результат; 
		ЭлементДанных.Формат.Длина = Макс(ЭлементДанных.Формат.Длина, СтрДлина(НаборСтрок[Индекс]));
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(Результат) = Тип("Массив") Тогда
		Для Каждого ЭлементРезультат Из Результат Цикл
			ЭлементДанных.Формат.Результат.Добавить(ЭлементРезультат);
			ЭлементДанных.Формат.Длина = Макс(ЭлементДанных.Формат.Длина, СтрДлина(ЭлементРезультат));
		КонецЦикла;
		Возврат;
	КонецЕсли;
	
	ЭлементДанных.Формат.Результат.Добавить(Результат);
	ЭлементДанных.Формат.Длина = Макс(ЭлементДанных.Формат.Длина, СтрДлина(Результат));
КонецПроцедуры

&НаКлиенте
Функция ФорматироватьГоризонтальноеПоложениеПодготовка(Строка, НаборСвойств, Длина)
	Результат = Новый Структура("Слева,Справа,Строка", "", "" , "");
	
	Если НЕ НаборСвойств.Свойство("ГоризонтальноеПоложениеВГруппе") Тогда 
		Результат.Строка = ВыровнитьСтроку(Строка, "Лево", Длина);
		Возврат Результат;
	КонецЕсли;

	ТекГоризонтальноеПоложение = НаборСвойств.ГоризонтальноеПоложениеВГруппе;
	
	СимволСлева = "";
	СимволСправа = "";
	Если ТекГоризонтальноеПоложение = "Право" Тогда
		СимволСлева = "-> ";
	КонецЕсли;
	
	Если ТекГоризонтальноеПоложение = "Центр" Тогда
		СимволСлева = "-> ";
		СимволСправа = " <-";
	КонецЕсли;
	
	Результат.Слева = СимволСлева;
	Результат.Справа = СимволСправа;
	Результат.Строка = ВыровнитьСтроку(Строка, ТекГоризонтальноеПоложение, Длина - СтрДлина(СимволСлева) - СтрДлина(СимволСправа));
	
	Возврат Результат;

КонецФункции

&НаКлиенте
Функция ФорматироватьГоризонтальноеПоложение(Строка, НаборСвойств, Длина)
	Результат = ФорматироватьГоризонтальноеПоложениеПодготовка(Строка, НаборСвойств, Длина);	
	Возврат Результат.Слева 
	    + Результат.Строка
		+ Результат.Справа;
КонецФункции

&НаКлиенте
Функция ПолучитьЗначениеСвойства(Структура, ИмяСвойства, ЗначениеПоУмолчанию = Неопределено)
	Если Не Структура.Свойство(ИмяСвойства) Тогда
		Возврат ЗначениеПоУмолчанию;
	КонецЕсли;
	
	Возврат Структура[ИмяСвойства];
КонецФункции

#КонецОбласти    